# tool: scribe-data
# Universal query to find grammatical feature combinations for a given language and data type.
# This query identifies form combinations that exist in Wikidata for the specified language/data type.
# Placeholders: {LANGUAGE_QID}, {DATA_TYPE_QID}, {MIN_FREQUENCY}, and {MAX_RESULTS} should be replaced with actual values.
# Format matches Wikidata query service standard (w.wiki/FesH).

SELECT ?comboQIDs (COUNT(?form) AS ?formsWithThisCombo)
WHERE {{
  {{
    SELECT ?form 
           (GROUP_CONCAT(DISTINCT REPLACE(STR(?feature), "^.*/(Q\\d+)$", "$1")) AS ?comboQIDs)
    WHERE {{
      ?lexeme dct:language wd:{LANGUAGE_QID};
              wikibase:lexicalCategory wd:{DATA_TYPE_QID};
              ontolex:lexicalForm ?form.
      ?form wikibase:grammaticalFeature ?feature.
    }}
    GROUP BY ?form
    LIMIT {MAX_RESULTS}
  }}
  FILTER(?comboQIDs != "")
}}
GROUP BY ?comboQIDs
HAVING(COUNT(?form) >= {MIN_FREQUENCY})
ORDER BY DESC(?formsWithThisCombo)
