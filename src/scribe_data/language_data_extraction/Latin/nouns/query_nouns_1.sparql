# tool: scribe-data
# All Latin (Q397) nouns in the given cases.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?noun
  ?nomSingular
  ?nomPlural

WHERE {
  ?lexeme dct:language wd:Q397 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun .

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomSingularForm .
    ?nomSingularForm ontolex:representation ?nomSingular ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .
}
