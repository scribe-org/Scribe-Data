# tool: scribe-data
# All Gurmukhi (from Punjabi Q58635) nouns, their plurals and their genders.
# Enter this query at https://query.wikidata.org/.

# Note: We need to filter for "pa" to select Gurmukhi words.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  ?plural
  ?gender

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and proper nouns

  ?lexeme dct:language wd:Q58635 ;
  wikibase:lexicalCategory ?nounTypes ;
  wikibase:lemma ?singular .
  FILTER(lang(?singular) = "pa")

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 ;
      FILTER(lang(?plural) = "pa")
  } .

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS {
      ?lexeme wdt:P31 wd:Q202444 .  # not for given names
    }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
