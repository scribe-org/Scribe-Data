# tool: scribe-data
# All Danish (Q9035) adjectives and some of the available forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?commonSingularIndefinite
  ?neuterSingularIndefinite

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Indefinite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?commonSingularIndefiniteForm .
    ?commonSingularIndefiniteForm ontolex:representation ?commonSingularIndefinite ;
      wikibase:grammaticalFeature wd:Q1305037 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q3482678 .
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularIndefiniteForm .
    ?neuterSingularIndefiniteForm ontolex:representation ?neuterSingularIndefinite ;
      wikibase:grammaticalFeature wd:Q1775461 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q3482678 .
  } .
}
