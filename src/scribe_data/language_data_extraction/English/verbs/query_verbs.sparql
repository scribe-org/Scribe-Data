# tool: scribe-data
# All English (Q1860) verbs and the currently implemented tenses for each.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presSimp
  ?presTPS
  ?presPart
  ?pastSimp
  ?pastPart

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q1860 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # MARK: Simple Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSimpForm .
    ?presSimpForm ontolex:representation ?presSimp ;
      wikibase:grammaticalFeature wd:Q3910936 ;
      FILTER NOT EXISTS { ?presSimpForm wikibase:grammaticalFeature wd:Q51929074 . }
      FILTER NOT EXISTS { ?presSimpForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?presSimpForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?presSimp) = "en") .
  } .

  # MARK: Third-person Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presTPSForm .
    ?presTPSForm ontolex:representation ?presTPS ;
    wikibase:grammaticalFeature wd:Q51929074 ;
    wikibase:grammaticalFeature wd:Q110786 ;
    wikibase:grammaticalFeature wd:Q3910936 ;
    FILTER NOT EXISTS { ?presTPSForm wdt:P6191 wd:Q181970 . }
    FILTER NOT EXISTS { ?presTPSForm wikibase:grammaticalFeature wd:Q126473 . }
    FILTER(LANG(?presTPS) = "en") .
  } .

  # MARK: Present Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presPartForm .
    ?presPartForm ontolex:representation ?presPart ;
    wikibase:grammaticalFeature wd:Q10345583 ;
    FILTER NOT EXISTS { ?presPartForm wdt:P6191 wd:Q181970 . }
    FILTER NOT EXISTS { ?presPartForm wikibase:grammaticalFeature wd:Q126473 . }
    FILTER(LANG(?presPart) = "en") .
  } .

  # MARK: Simple Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastSimpForm .
    ?pastSimpForm ontolex:representation ?pastSimp ;
    wikibase:grammaticalFeature wd:Q1392475 ;
    FILTER NOT EXISTS { ?pastSimpForm wdt:P6191 wd:Q181970 . }
    FILTER NOT EXISTS { ?pastSimpForm wikibase:grammaticalFeature wd:Q126473 . }
    FILTER(LANG(?pastSimp) = "en") .
  } .

  # MARK: Past Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastPartForm .
    ?pastPartForm ontolex:representation ?pastPart ;
    wikibase:grammaticalFeature wd:Q1230649 ;
    FILTER NOT EXISTS { ?pastPartForm wdt:P6191 wd:Q181970 . }
    FILTER NOT EXISTS { ?pastPartForm wikibase:grammaticalFeature wd:Q126473 . }
    FILTER(LANG(?pastPart) = "en") .
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
  }
}
