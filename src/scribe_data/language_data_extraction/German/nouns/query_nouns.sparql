# tool: scribe-data
# All German (Q188) nouns and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nomSingular
  ?nomPlural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?nomSingular .

  # MARK: Nominative Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q131105 ;
  } .

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
