# tool: scribe-data
# All Esperanto (Q143) proper nouns and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nomSingular
  ?accSingular
  ?nomPlural
  ?accPlural

WHERE {
  ?lexeme dct:language wd:Q143 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?nomSingular .

  # MARK: Accusative Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accSingularForm .
    ?accSingularForm ontolex:representation ?accSingular ;
      wikibase:grammaticalFeature wd:Q146078 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  # MARK: Nominative Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  # MARK: Accusative Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accPluralForm .
    ?accPluralForm ontolex:representation ?accPlural ;
      wikibase:grammaticalFeature wd:Q146078 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .
}
