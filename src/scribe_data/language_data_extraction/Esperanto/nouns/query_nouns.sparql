# tool: scribe-data
# All Esperanto (Q143) nouns and their plurals for the given cases.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nomSingular
  ?accSingular
  ?nomPlural
  ?accPlural

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and pronouns
  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q143 ;
    wikibase:lexicalCategory ?nounType ;
    wikibase:lemma ?nomSingular .

  FILTER(?nounType = ?nounTypes)

  # Optional selection of accusative singular forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accSingularForm .
    ?accSingularForm ontolex:representation ?accSingular ;
      wikibase:grammaticalFeature wd:Q146078 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  # Optional selection of nominative plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nomPluralForm .
    ?nomPluralForm ontolex:representation ?nomPlural ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  # Optional selection of accusative plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accPluralForm .
    ?accPluralForm ontolex:representation ?accPlural ;
      wikibase:grammaticalFeature wd:Q146078 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .
}
