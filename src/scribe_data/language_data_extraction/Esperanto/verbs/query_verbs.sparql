# tool: scribe-data
# All Esperanto (Q143) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presIndicative
  ?pastIndicative
  ?futIndicative
  ?conditional
  ?volitive

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q143 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # MARK: Present Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presIndicativeForm .
    ?presIndicativeForm ontolex:representation ?presIndicative ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q682111 .
      FILTER(LANG(?presIndicative) = "eo") .
  }

  # MARK: Past Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastIndicativeForm .
    ?pastIndicativeForm ontolex:representation ?pastIndicative ;
      wikibase:grammaticalFeature wd:Q1994301, wd:Q682111 ;
      FILTER(LANG(?pastIndicative) = "eo") .
  }

  # MARK: Future Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futIndicativeForm .
    ?futIndicativeForm ontolex:representation ?futIndicative ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q682111 ;
      FILTER(LANG(?futIndicative) = "eo") .
  }

  # MARK: Conditional

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalForm .
    ?conditionalForm ontolex:representation ?conditional ;
      wikibase:grammaticalFeature wd:Q625581 ;
      FILTER(LANG(?conditional) = "eo") .
  }

  # MARK: Volitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?volitiveForm .
    ?volitiveForm ontolex:representation ?volitive ;
      wikibase:grammaticalFeature wd:Q2532941 ;
      FILTER(LANG(?volitive) = "eo") .
  }
}
