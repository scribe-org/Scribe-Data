# tool: scribe-data
# All Arabic (Q13955) verbs and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?impSPSM ?impSPSF ?impSPD
  ?impSPPM ?impSPPF

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Imperative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPSMForm .
    ?impSPSMForm ontolex:representation ?impSPSM ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q499327, wd:Q682111, wd:Q12230930
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPSFForm .
    ?impSPSFForm ontolex:representation ?impSPSF ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q1775415, wd:Q682111, wd:Q12230930
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPDForm .
    ?impSPDForm ontolex:representation ?impSPD ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110022, wd:Q682111, wd:Q12230930
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPPMForm .
    ?impSPPMForm ontolex:representation ?impSPPM ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q499327, wd:Q682111, wd:Q12230930
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?impSPPFForm .
    ?impSPPFForm ontolex:representation ?impSPPF ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q1775415, wd:Q682111, wd:Q12230930
  } .
}
