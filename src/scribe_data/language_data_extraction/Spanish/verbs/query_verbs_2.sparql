# tool: scribe-data
# All Spanish (Q1321) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?pretFPS ?pretSPS ?pretTPS
  ?pretFPP ?pretSPP ?pretTPP

WHERE {
  ?lexeme dct:language wd:Q1321 ;
    wikibase:lexicalCategory wd:Q24905 .

  # MARK: Infinitive
  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 ;

  # MARK: Preterite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPSForm .
    ?pretFPSForm ontolex:representation ?pretFPS ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPSForm .
    ?pretSPSForm ontolex:representation ?pretSPS ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPSForm .
    ?pretTPSForm ontolex:representation ?pretTPS ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretFPPForm .
    ?pretFPPForm ontolex:representation ?pretFPP ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretSPPForm .
    ?pretSPPForm ontolex:representation ?pretSPP ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pretTPPForm .
    ?pretTPPForm ontolex:representation ?pretTPP ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786, wd:Q442485 .
  }
}
