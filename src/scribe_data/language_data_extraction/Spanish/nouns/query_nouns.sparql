# tool: scribe-data
# All Spanish (Q1321) nouns, their plurals and their genders.
# Enter this query at https://query.wikidata.org/.
# Note: Spansih sometimes has masculine and feminine versions on a single lexeme.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  ?plural
  ?gender
  ?masSingular
  ?masPlural
  ?femSingular
  ?femPlural

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and proper nouns

  ?lexeme dct:language wd:Q1321 ;
    wikibase:lexicalCategory ?nounTypes ;
    wikibase:lemma ?singular .

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS {
      ?lexeme wdt:P31 wd:Q202444 .  # not for given names
    }
  } .

  # Spansih sometimes has masculine and feminine versions on a single lexeme.

  # MARK: masculine singular and plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masSingularForm .
    ?masSingularForm ontolex:representation ?masSingular ;
      wikibase:grammaticalFeature wd:Q499327 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masPluralForm .
    ?masPluralForm ontolex:representation ?masPlural ;
      wikibase:grammaticalFeature wd:Q499327 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  # MARK: feminine singular and plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femSingularForm .
    ?femSingularForm ontolex:representation ?femSingular ;
      wikibase:grammaticalFeature wd:Q1775415 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femPluralForm .
    ?femPluralForm ontolex:representation ?femPlural ;
      wikibase:grammaticalFeature wd:Q1775415 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
