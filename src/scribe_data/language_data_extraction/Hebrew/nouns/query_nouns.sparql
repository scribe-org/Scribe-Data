# tool: scribe-data
# All Hebrew (Q9288) nouns, their plurals and their genders.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?noun
  ?plural
  ?gender

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and proper nouns

  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory ?nounTypes ;
    wikibase:lemma ?noun .
    FILTER(lang(?noun) = "he")

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 ;
      FILTER(lang(?plural) = "he")
  } .

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS {
      ?lexeme wdt:P31 wd:Q202444 .  # not for given names
    }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
