# tool: scribe-data
# All Hebrew (Q9288) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?presSF
  ?presSM
  ?presPF
  ?presPM

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .
    FILTER(lang(?infinitive) = "he")

  # MARK: Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSFForm .
    ?presSFForm ontolex:representation ?presSF ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q192613, wd:Q1775415 .
      FILTER(lang(?presSF) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presSMForm .
    ?presSMForm ontolex:representation ?presSM ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q192613, wd:Q499327 .
      FILTER(lang(?presSM) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presPFForm .
    ?presPFForm ontolex:representation ?presPF ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q192613, wd:Q1775415 .
      FILTER(lang(?presPF) = "he") .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presPMForm .
    ?presPMForm ontolex:representation ?presPM ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q192613, wd:Q499327 .
      FILTER(lang(?presPM) = "he") .
  }
}
