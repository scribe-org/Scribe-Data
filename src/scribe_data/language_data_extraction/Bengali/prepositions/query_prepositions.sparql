# tool: scribe-data
# All Bengali (Bangla Q9610) prepositions and their corresponding cases.
# Enter this query at https://query.wikidata.org/.
# Note: This query includes postpositions that are also used in Bengali.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?preposition
  ?case

WHERE {
  # Prepositions and postpositions.
  VALUES ?prePostPositions { wd:Q4833830 wd:Q161873 }

  ?lexeme dct:language wd:Q9610 ;
    wikibase:lexicalCategory ?prePostPositions ;
    wikibase:lemma ?preposition .

  # MARK: Corresponding Case

  OPTIONAL {
    ?lexeme wdt:P5713 ?caseForm .
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?caseForm rdfs:label ?case .
  }
}
