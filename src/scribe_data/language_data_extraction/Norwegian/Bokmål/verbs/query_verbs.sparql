# tool: scribe-data
# All Norwegian Bokmål (Q9043) verbs and the currently implemented tenses for each.
# Enter this query at https://query.wikidata.org/.
# Note: This query is for Bokmål (Q25167) rather than Nynorsk (Q25164).

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?present

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 .

  # MARK: Active Infinitive

  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 ;
    wikibase:grammaticalFeature wd:Q1317831 .

  # MARK: Active Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 ;
      wikibase:grammaticalFeature wd:Q1317831 .
  }
}
