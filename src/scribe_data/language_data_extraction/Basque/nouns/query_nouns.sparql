# tool: scribe-data
# All Basque (Q8752) nouns and all implemented singular and plural forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?absIndefinite
  ?absSingular
  ?absPlural

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and proper nouns

  ?lexeme dct:language wd:Q8752 ;
    wikibase:lexicalCategory ?nounTypes ;
    wikibase:lemma ?absIndefinite .

  # MARK: Absolutive Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absSingularForm .
    ?absSingularForm ontolex:representation ?absSingular ;
      wikibase:grammaticalFeature wd:Q332734 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  # MARK: Absolutive Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absPluralForm .
    ?absPluralForm ontolex:representation ?absPlural ;
      wikibase:grammaticalFeature wd:Q332734 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .
}
