# tool: scribe-data
# All Basque (Q8752) nouns and all implemented singular and plural forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?absIndefinite
  ?absSingular
  ?absPlural

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and pronouns
  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q8752 ;
    wikibase:lexicalCategory ?nounType ;
    wikibase:lemma ?absIndefinite .

  FILTER(?nounType = ?nounTypes)

  # Optional selection of absolutive singular forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absSingularForm .
    ?absSingularForm ontolex:representation ?absSingular ;
      wikibase:grammaticalFeature wd:Q332734 ;
      wikibase:grammaticalFeature wd:Q110786 ;
  } .

  # Optional selection of absolutive plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absPluralForm .
    ?absPluralForm ontolex:representation ?absPlural ;
      wikibase:grammaticalFeature wd:Q332734 ;
      wikibase:grammaticalFeature wd:Q146786 ;
  } .
}
