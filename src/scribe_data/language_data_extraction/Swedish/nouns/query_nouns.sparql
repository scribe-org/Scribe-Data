# tool: scribe-data
# All Swedish (Q9027) nouns with their plural, gender and genitive forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nomIndefSingular
  ?nomIndefPlural
  ?genIndefSingular
  ?genIndefPlural
  ?nomDefSingular
  ?nomDefPlural
  ?genDefSingular
  ?genDefPlural
  ?gender

WHERE {
  VALUES ?nounTypes {wd:Q1084 wd:Q147276} # Nouns and proper nouns

  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory ?nounTypes .

  # MARK: Indefinite

  OPTIONAL {
    # Nominative Singular
    ?lexeme ontolex:lexicalForm ?nomIndefSingularForm .
    ?nomIndefSingularForm ontolex:representation ?nomIndefSingular ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Nominative Plural
    ?lexeme ontolex:lexicalForm ?nomIndefPluralForm .
    ?nomIndefPluralForm ontolex:representation ?nomIndefPlural ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .

    # Genitive Singular
    ?lexeme ontolex:lexicalForm ?genIndefSingularForm .
    ?genIndefSingularForm ontolex:representation ?genIndefSingular ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Genitive Plural
    ?lexeme ontolex:lexicalForm ?genIndefPluralForm .
    ?genIndefPluralForm ontolex:representation ?genIndefPlural ;
      wikibase:grammaticalFeature wd:Q53997857 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # MARK: Definite

  OPTIONAL {
    # Nominative Singular
    ?lexeme ontolex:lexicalForm ?nomDefSingularForm .
    ?nomDefSingularForm ontolex:representation ?nomDefSingular ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Nominative Plural
    ?lexeme ontolex:lexicalForm ?nomDefPluralForm .
    ?nomDefPluralForm ontolex:representation ?nomDefPlural ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q131105 ;
      wikibase:grammaticalFeature wd:Q146786 .

    # Genitive Singular
    ?lexeme ontolex:lexicalForm ?genDefSingularForm .
    ?genDefSingularForm ontolex:representation ?genDefSingular ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q110786 .

    # Genitive Plural
    ?lexeme ontolex:lexicalForm ?genDefPluralForm .
    ?genDefPluralForm ontolex:representation ?genDefPlural ;
      wikibase:grammaticalFeature wd:Q53997851 ;
      wikibase:grammaticalFeature wd:Q146233 ;
      wikibase:grammaticalFeature wd:Q146786 .
  } .

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
    FILTER NOT EXISTS {
      ?lexeme wdt:P31 wd:Q202444 .  # not for given names
    }
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
