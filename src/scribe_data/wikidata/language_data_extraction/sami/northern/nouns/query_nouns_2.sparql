# tool: scribe-data
# All Sami (Q33947) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?accusativeSingular
  ?accusativePlural
  ?illativeSingular
  ?illativePlural
  ?comitativeSingular
  ?comitativePlural

WHERE {
  ?lexeme dct:language wd:Q33947 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for se to remove northern (se) words.
  FILTER(lang(?noun) = "se")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularForm .
    ?accusativeSingularForm ontolex:representation ?accusativeSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativeSingularForm .
    ?illativeSingularForm ontolex:representation ?illativeSingular ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativePluralForm .
    ?illativePluralForm ontolex:representation ?illativePlural ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativeSingularForm .
    ?comitativeSingularForm ontolex:representation ?comitativeSingular ;
      wikibase:grammaticalFeature wd:Q838581, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativePluralForm .
    ?comitativePluralForm ontolex:representation ?comitativePlural ;
      wikibase:grammaticalFeature wd:Q838581, wd:Q146786 .
  }
}
