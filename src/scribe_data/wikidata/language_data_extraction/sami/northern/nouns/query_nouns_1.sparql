# tool: scribe-data
# All Sami (Q33947) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?essive
  ?genitiveSingular
  ?genitivePlural
  ?locativeSingular
  ?locativePlural

WHERE {
  ?lexeme dct:language wd:Q33947 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for se to remove northern (se) words.
  FILTER(lang(?noun) = "se")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?essiveForm .
    ?essiveForm ontolex:representation ?essive ;
      wikibase:grammaticalFeature wd:Q148465 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeSingularForm .
    ?locativeSingularForm ontolex:representation ?locativeSingular ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativePluralForm .
    ?locativePluralForm ontolex:representation ?locativePlural ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q146786 .
  }
}
