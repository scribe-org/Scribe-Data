# tool: scribe-data
# All Punjabi (Q58635) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?secondPersonSingular
  ?secondPersonPlural
  ?secondPersonSingularSubjunctive
  ?secondPersonPluralSubjunctive

WHERE {
  ?lexeme dct:language wd:Q58635 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  # Note: We need to filter for pnb to remove shahmukhi (pnb) words.
  FILTER(lang(?verb) = "pnb")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingularForm .
    ?secondPersonSingularForm ontolex:representation ?secondPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralForm .
    ?secondPersonPluralForm ontolex:representation ?secondPersonPlural ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingularSubjunctiveForm .
    ?secondPersonSingularSubjunctiveForm ontolex:representation ?secondPersonSingularSubjunctive ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralSubjunctiveForm .
    ?secondPersonPluralSubjunctiveForm ontolex:representation ?secondPersonPluralSubjunctive ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q473746 .
  }
}
