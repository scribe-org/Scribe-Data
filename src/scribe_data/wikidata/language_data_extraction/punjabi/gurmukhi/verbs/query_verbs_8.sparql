# tool: scribe-data
# All Punjabi (Q58635) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?firstPersonSingular
  ?firstPersonPlural
  ?firstPersonSingularSubjunctive
  ?firstPersonPluralSubjunctive

WHERE {
  ?lexeme dct:language wd:Q58635 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  # Note: We need to filter for pa to remove gurmukhi (pa) words.
  FILTER(lang(?verb) = "pa")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingularForm .
    ?firstPersonSingularForm ontolex:representation ?firstPersonSingular ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralForm .
    ?firstPersonPluralForm ontolex:representation ?firstPersonPlural ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingularSubjunctiveForm .
    ?firstPersonSingularSubjunctiveForm ontolex:representation ?firstPersonSingularSubjunctive ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralSubjunctiveForm .
    ?firstPersonPluralSubjunctiveForm ontolex:representation ?firstPersonPluralSubjunctive ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786, wd:Q473746 .
  }
}
