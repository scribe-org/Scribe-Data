# tool: scribe-data
# All Punjabi (Q58635) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?singularMasculineDirect
  ?singularMasculineOblique

WHERE {
  ?lexeme dct:language wd:Q58635 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for pa to remove gurmukhi (pa) words.
  FILTER(lang(?propernoun) = "pa")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineDirectForm .
    ?singularMasculineDirectForm ontolex:representation ?singularMasculineDirect ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327, wd:Q1751855 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineObliqueForm .
    ?singularMasculineObliqueForm ontolex:representation ?singularMasculineOblique ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327, wd:Q1233197 .
  }
}
