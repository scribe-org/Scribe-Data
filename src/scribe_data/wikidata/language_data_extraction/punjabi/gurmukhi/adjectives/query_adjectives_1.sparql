# tool: scribe-data
# All Punjabi (Q58635) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?comparative
  ?vocativePlural
  ?directSingular
  ?directPlural
  ?obliqueSingular
  ?obliquePlural

WHERE {
  ?lexeme dct:language wd:Q58635 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .

  # Note: We need to filter for pa to remove gurmukhi (pa) words.
  FILTER(lang(?adjective) = "pa")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativePluralForm .
    ?vocativePluralForm ontolex:representation ?vocativePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directSingularForm .
    ?directSingularForm ontolex:representation ?directSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directPluralForm .
    ?directPluralForm ontolex:representation ?directPlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueSingularForm .
    ?obliqueSingularForm ontolex:representation ?obliqueSingular ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliquePluralForm .
    ?obliquePluralForm ontolex:representation ?obliquePlural ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q146786 .
  }
}
