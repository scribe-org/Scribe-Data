# tool: scribe-data
# All Igbo (Q33578) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?oblique
  ?obliqueThirdPersonPlural

WHERE {
  ?lexeme dct:language wd:Q33578 ;
    wikibase:lexicalCategory wd:Q36224 ;
    wikibase:lemma ?pronoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueForm .
    ?obliqueForm ontolex:representation ?oblique ;
      wikibase:grammaticalFeature wd:Q1233197 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueThirdPersonPluralForm .
    ?obliqueThirdPersonPluralForm ontolex:representation ?obliqueThirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q51929074, wd:Q146786 .
  }
}
