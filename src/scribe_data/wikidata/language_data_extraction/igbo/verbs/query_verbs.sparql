# tool: scribe-data
# All Igbo (Q33578) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?present
  ?past
  ?combinedPresentParticiple

WHERE {
  ?lexeme dct:language wd:Q33578 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }
}
