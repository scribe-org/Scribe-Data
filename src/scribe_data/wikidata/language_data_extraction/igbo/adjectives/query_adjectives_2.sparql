# tool: scribe-data
# All Igbo (Q33578) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?present
  ?combinedPresentParticiple
  ?singularPlural

WHERE {
  ?lexeme dct:language wd:Q33578 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPluralForm .
    ?singularPluralForm ontolex:representation ?singularPlural ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146786 .
  }
}
