# tool: scribe-data
# All Hebrew (Q9288) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?commonSingular
  ?futureFirstPersonSingular
  ?futureFirstPersonPlural
  ?feminineImperativeSecondPersonSingular
  ?feminineImperativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?commonSingularForm .
    ?commonSingularForm ontolex:representation ?commonSingular ;
      wikibase:grammaticalFeature wd:Q1305037, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureFirstPersonSingularForm .
    ?futureFirstPersonSingularForm ontolex:representation ?futureFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureFirstPersonPluralForm .
    ?futureFirstPersonPluralForm ontolex:representation ?futureFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineImperativeSecondPersonSingularForm .
    ?feminineImperativeSecondPersonSingularForm ontolex:representation ?feminineImperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineImperativeSecondPersonPluralForm .
    ?feminineImperativeSecondPersonPluralForm ontolex:representation ?feminineImperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q22716, wd:Q51929049, wd:Q146786 .
  }
}
