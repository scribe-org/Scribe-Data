# tool: scribe-data
# All Hebrew (Q9288) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?feminineImperativeSecondPersonSingular
  ?feminineImperativeSecondPersonPlural
  ?masculineImperativeSecondPersonSingular
  ?masculineImperativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q24905 .

  # MARK: Imerpative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineImperativeSecondPersonSingularForm .
    ?feminineImperativeSecondPersonSingularForm ontolex:representation ?feminineImperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q22716, wd:Q51929049, wd:Q110786 .
      FILTER(lang(?feminineImperativeSecondPersonSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineImperativeSecondPersonPluralForm .
    ?feminineImperativeSecondPersonPluralForm ontolex:representation ?feminineImperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q22716, wd:Q51929049, wd:Q146786 .
      FILTER(lang(?feminineImperativeSecondPersonPlural) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineImperativeSecondPersonSingularForm .
    ?masculineImperativeSecondPersonSingularForm ontolex:representation ?masculineImperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q22716, wd:Q51929049, wd:Q110786 .
      FILTER(lang(?masculineImperativeSecondPersonSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineImperativeSecondPersonPluralForm .
    ?masculineImperativeSecondPersonPluralForm ontolex:representation ?masculineImperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q22716, wd:Q51929049, wd:Q146786 .
      FILTER(lang(?masculineImperativeSecondPersonPlural) = "he")
  }
}
