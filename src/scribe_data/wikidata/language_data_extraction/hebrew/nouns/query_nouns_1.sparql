# tool: scribe-data
# All Hebrew (Q9288) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?feminine
  ?masculine
  ?femininePlural
  ?pluralConstruct
  ?femininePluralConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineForm .
    ?feminineForm ontolex:representation ?feminine ;
      wikibase:grammaticalFeature wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineForm .
    ?masculineForm ontolex:representation ?masculine ;
      wikibase:grammaticalFeature wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralForm .
    ?femininePluralForm ontolex:representation ?femininePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralConstructForm .
    ?pluralConstructForm ontolex:representation ?pluralConstruct ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralConstructForm .
    ?femininePluralConstructForm ontolex:representation ?femininePluralConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1641446 .
  }
}
