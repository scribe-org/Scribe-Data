# tool: scribe-data
# All Hebrew (Q9288) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominativeSingular
  ?nominativePlural
  ?masculineSingular
  ?masculineDual
  ?masculineSingularConstruct
  ?masculineDualConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineDualForm .
    ?masculineDualForm ontolex:representation ?masculineDual ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularConstructForm .
    ?masculineSingularConstructForm ontolex:representation ?masculineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineDualConstructForm .
    ?masculineDualConstructForm ontolex:representation ?masculineDualConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110022, wd:Q1641446 .
  }
}
