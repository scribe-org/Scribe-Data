# tool: scribe-data
# All Malay (Q9237) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?passive
  ?active
  ?phrase
  ?passivePhrase

WHERE {
  ?lexeme dct:language wd:Q9237 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveForm .
    ?passiveForm ontolex:representation ?passive ;
      wikibase:grammaticalFeature wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeForm .
    ?activeForm ontolex:representation ?active ;
      wikibase:grammaticalFeature wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?phraseForm .
    ?phraseForm ontolex:representation ?phrase ;
      wikibase:grammaticalFeature wd:Q187931 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePhraseForm .
    ?passivePhraseForm ontolex:representation ?passivePhrase ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q187931 .
  }
}
