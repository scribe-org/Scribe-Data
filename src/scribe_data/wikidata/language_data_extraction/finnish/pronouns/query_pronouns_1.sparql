# tool: scribe-data
# All Finnish (Q1412) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?nominative
  ?inessive
  ?nominativeSingular
  ?nominativePlural
  ?inessiveSingular
  ?inessivePlural

WHERE {
  ?lexeme dct:language wd:Q1412 ;
      wikibase:lexicalCategory wd:Q36224 ;
      wikibase:lemma ?pronoun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessiveForm .
    ?inessiveForm ontolex:representation ?inessive ;
      wikibase:grammaticalFeature wd:Q282031 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessiveSingularForm .
    ?inessiveSingularForm ontolex:representation ?inessiveSingular ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessivePluralForm .
    ?inessivePluralForm ontolex:representation ?inessivePlural ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q146786 .
  }
}
