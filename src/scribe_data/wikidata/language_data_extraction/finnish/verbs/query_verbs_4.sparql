# tool: scribe-data
# All Finnish (Q1412) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?presentFirstPersonSingular
  ?presentSecondPersonSingular
  ?pastImperfectFirstPersonSingular
  ?pastImperfectSecondPersonSingular
  ?imperativePresentSecondPersonSingular
  ?imperativePresentThirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1412 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentFirstPersonSingularForm .
    ?presentFirstPersonSingularForm ontolex:representation ?presentFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentSecondPersonSingularForm .
    ?presentSecondPersonSingularForm ontolex:representation ?presentSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastImperfectFirstPersonSingularForm .
    ?pastImperfectFirstPersonSingularForm ontolex:representation ?pastImperfectFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q12547192, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastImperfectSecondPersonSingularForm .
    ?pastImperfectSecondPersonSingularForm ontolex:representation ?pastImperfectSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q12547192, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePresentSecondPersonSingularForm .
    ?imperativePresentSecondPersonSingularForm ontolex:representation ?imperativePresentSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q192613, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePresentThirdPersonSingularForm .
    ?imperativePresentThirdPersonSingularForm ontolex:representation ?imperativePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q192613, wd:Q51929074, wd:Q110786 .
  }
}
