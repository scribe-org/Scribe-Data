# tool: scribe-data
# All Finnish (Q1412) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?partitiveSingular
  ?partitivePlural
  ?inessiveSingular
  ?inessivePlural
  ?adessiveSingular
  ?adessivePlural

WHERE {
  ?lexeme dct:language wd:Q1412 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitiveSingularForm .
    ?partitiveSingularForm ontolex:representation ?partitiveSingular ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitivePluralForm .
    ?partitivePluralForm ontolex:representation ?partitivePlural ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessiveSingularForm .
    ?inessiveSingularForm ontolex:representation ?inessiveSingular ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessivePluralForm .
    ?inessivePluralForm ontolex:representation ?inessivePlural ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adessiveSingularForm .
    ?adessiveSingularForm ontolex:representation ?adessiveSingular ;
      wikibase:grammaticalFeature wd:Q281954, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adessivePluralForm .
    ?adessivePluralForm ontolex:representation ?adessivePlural ;
      wikibase:grammaticalFeature wd:Q281954, wd:Q146786 .
  }
}
