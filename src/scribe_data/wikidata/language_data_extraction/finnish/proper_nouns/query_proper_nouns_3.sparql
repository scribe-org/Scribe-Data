# tool: scribe-data
# All Finnish (Q1412) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?genitiveSingular
  ?genitivePlural
  ?adessiveSingular
  ?adessivePlural
  ?translativeSingular
  ?translativePlural

WHERE {
  ?lexeme dct:language wd:Q1412 ;
      wikibase:lexicalCategory wd:Q147276 ;
      wikibase:lemma ?propernoun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adessiveSingularForm .
    ?adessiveSingularForm ontolex:representation ?adessiveSingular ;
      wikibase:grammaticalFeature wd:Q281954, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adessivePluralForm .
    ?adessivePluralForm ontolex:representation ?adessivePlural ;
      wikibase:grammaticalFeature wd:Q281954, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?translativeSingularForm .
    ?translativeSingularForm ontolex:representation ?translativeSingular ;
      wikibase:grammaticalFeature wd:Q950170, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?translativePluralForm .
    ?translativePluralForm ontolex:representation ?translativePlural ;
      wikibase:grammaticalFeature wd:Q950170, wd:Q146786 .
  }
}
