# tool: scribe-data
# All Norwegian (Q25164) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?nominative
  ?genitive
  ?indefiniteSingular
  ?definiteSingular
  ?definitePlural

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?propernoun) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefiniteSingularForm .
    ?indefiniteSingularForm ontolex:representation ?indefiniteSingular ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definitePluralForm .
    ?definitePluralForm ontolex:representation ?definitePlural ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q146786 .
  }
}
