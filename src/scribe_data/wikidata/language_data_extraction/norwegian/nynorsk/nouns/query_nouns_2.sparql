# tool: scribe-data
# All Norwegian (Q25164) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?indefiniteSingular
  ?feminineIndefinitePlural
  ?feminineDefiniteSingular
  ?feminineDefinitePlural

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?noun) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefiniteSingularForm .
    ?indefiniteSingularForm ontolex:representation ?indefiniteSingular ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineIndefinitePluralForm .
    ?feminineIndefinitePluralForm ontolex:representation ?feminineIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineDefiniteSingularForm .
    ?feminineDefiniteSingularForm ontolex:representation ?feminineDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineDefinitePluralForm .
    ?feminineDefinitePluralForm ontolex:representation ?feminineDefinitePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997851, wd:Q146786 .
  }
}
