# tool: scribe-data
# All Norwegian (Q25164) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?presentPassive
  ?presentActive
  ?presentPreteritePerfect
  ?singularCombinedPastParticipleDefinite
  ?singularNeuterCombinedPastParticipleIndefinite
  ?singularMasculineFeminineCombinedPastParticipleIndefinite

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?verb) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPassiveForm .
    ?presentPassiveForm ontolex:representation ?presentPassive ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentActiveForm .
    ?presentActiveForm ontolex:representation ?presentActive ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPreteritePerfectForm .
    ?presentPreteritePerfectForm ontolex:representation ?presentPreteritePerfect ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q442485, wd:Q625420 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularCombinedPastParticipleDefiniteForm .
    ?singularCombinedPastParticipleDefiniteForm ontolex:representation ?singularCombinedPastParticipleDefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q12717679, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularNeuterCombinedPastParticipleIndefiniteForm .
    ?singularNeuterCombinedPastParticipleIndefiniteForm ontolex:representation ?singularNeuterCombinedPastParticipleIndefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1775461, wd:Q12717679, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineFeminineCombinedPastParticipleIndefiniteForm .
    ?singularMasculineFeminineCombinedPastParticipleIndefiniteForm ontolex:representation ?singularMasculineFeminineCombinedPastParticipleIndefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327, wd:Q1775415, wd:Q12717679, wd:Q53997857 .
  }
}
