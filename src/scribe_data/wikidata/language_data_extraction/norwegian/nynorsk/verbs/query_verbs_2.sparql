# tool: scribe-data
# All Norwegian (Q25164) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?present
  ?pluralCombinedPastParticiple
  ?presentPreteritePerfect
  ?feminineMasculineIndefiniteSingularCombinedPastParticiple

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?verb) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralCombinedPastParticipleForm .
    ?pluralCombinedPastParticipleForm ontolex:representation ?pluralCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPreteritePerfectForm .
    ?presentPreteritePerfectForm ontolex:representation ?presentPreteritePerfect ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q442485, wd:Q625420 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularCombinedPastParticipleForm .
    ?feminineMasculineIndefiniteSingularCombinedPastParticipleForm ontolex:representation ?feminineMasculineIndefiniteSingularCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786, wd:Q12717679 .
  }
}
