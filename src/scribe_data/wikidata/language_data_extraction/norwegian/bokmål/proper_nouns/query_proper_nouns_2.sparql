# tool: scribe-data
# All Norwegian (Q25167) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?indefiniteSingular
  ?indefinitePlural

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?propernoun) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefiniteSingularForm .
    ?indefiniteSingularForm ontolex:representation ?indefiniteSingular ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefinitePluralForm .
    ?indefinitePluralForm ontolex:representation ?indefinitePlural ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q146786 .
  }
}
