# tool: scribe-data
# All Norwegian (Q25167) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?indefiniteSingular
  ?indefinitePlural
  ?feminineIndefiniteSingular
  ?feminineMasculineIndefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?adjective) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefiniteSingularForm .
    ?indefiniteSingularForm ontolex:representation ?indefiniteSingular ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefinitePluralForm .
    ?indefinitePluralForm ontolex:representation ?indefinitePlural ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineIndefiniteSingularForm .
    ?feminineIndefiniteSingularForm ontolex:representation ?feminineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularForm .
    ?feminineMasculineIndefiniteSingularForm ontolex:representation ?feminineMasculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786 .
  }
}
