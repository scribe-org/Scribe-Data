# tool: scribe-data
# All Norwegian (Q25167) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?singularDefinite
  ?singularNeuterIndefinite
  ?singularMasculineFeminineIndefinite

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?adjective) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularDefiniteForm .
    ?singularDefiniteForm ontolex:representation ?singularDefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularNeuterIndefiniteForm .
    ?singularNeuterIndefiniteForm ontolex:representation ?singularNeuterIndefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1775461, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineFeminineIndefiniteForm .
    ?singularMasculineFeminineIndefiniteForm ontolex:representation ?singularMasculineFeminineIndefinite ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327, wd:Q1775415, wd:Q53997857 .
  }
}
