# tool: scribe-data
# All Norwegian (Q25167) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?definite
  ?definiteSingular
  ?definitePlural
  ?definiteSuperlative

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?adjective) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteForm .
    ?definiteForm ontolex:representation ?definite ;
      wikibase:grammaticalFeature wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definitePluralForm .
    ?definitePluralForm ontolex:representation ?definitePlural ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSuperlativeForm .
    ?definiteSuperlativeForm ontolex:representation ?definiteSuperlative ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q1817208 .
  }
}
