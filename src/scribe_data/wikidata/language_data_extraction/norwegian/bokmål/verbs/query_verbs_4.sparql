# tool: scribe-data
# All Norwegian (Q25167) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?passiveImperative
  ?passivePreterite
  ?passivePresentPerfect
  ?passiveActiveInfinitive
  ?passiveActivePresent

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?verb) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveImperativeForm .
    ?passiveImperativeForm ontolex:representation ?passiveImperative ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePreteriteForm .
    ?passivePreteriteForm ontolex:representation ?passivePreterite ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePresentPerfectForm .
    ?passivePresentPerfectForm ontolex:representation ?passivePresentPerfect ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q1240211 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveActiveInfinitiveForm .
    ?passiveActiveInfinitiveForm ontolex:representation ?passiveActiveInfinitive ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q1317831, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveActivePresentForm .
    ?passiveActivePresentForm ontolex:representation ?passiveActivePresent ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q1317831, wd:Q192613 .
  }
}
