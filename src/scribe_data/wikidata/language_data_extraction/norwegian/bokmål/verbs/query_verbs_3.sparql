# tool: scribe-data
# All Norwegian (Q25167) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperative
  ?combinedPresentParticiple
  ?passiveActiveInfinitive
  ?passiveActivePresent
  ?feminineMasculineIndefiniteSingularCombinedPastParticiple

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?verb) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveActiveInfinitiveForm .
    ?passiveActiveInfinitiveForm ontolex:representation ?passiveActiveInfinitive ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q1317831, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveActivePresentForm .
    ?passiveActivePresentForm ontolex:representation ?passiveActivePresent ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q1317831, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularCombinedPastParticipleForm .
    ?feminineMasculineIndefiniteSingularCombinedPastParticipleForm ontolex:representation ?feminineMasculineIndefiniteSingularCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786, wd:Q12717679 .
  }
}
