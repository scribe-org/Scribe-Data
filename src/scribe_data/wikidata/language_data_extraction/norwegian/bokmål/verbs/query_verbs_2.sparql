# tool: scribe-data
# All Norwegian (Q25167) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitivePassive
  ?presentPassive
  ?infinitiveActive
  ?presentActive

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?verb) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitivePassiveForm .
    ?infinitivePassiveForm ontolex:representation ?infinitivePassive ;
      wikibase:grammaticalFeature wd:Q179230, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPassiveForm .
    ?presentPassiveForm ontolex:representation ?presentPassive ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveActiveForm .
    ?infinitiveActiveForm ontolex:representation ?infinitiveActive ;
      wikibase:grammaticalFeature wd:Q179230, wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentActiveForm .
    ?presentActiveForm ontolex:representation ?presentActive ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q1317831 .
  }
}
