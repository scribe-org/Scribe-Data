# tool: scribe-data
# All Norwegian (Q25167) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?present
  ?preterite
  ?presentPerfect
  ?definiteSingularCombinedPastParticiple
  ?neuterIndefiniteSingularCombinedPastParticiple

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?verb) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteForm .
    ?preteriteForm ontolex:representation ?preterite ;
      wikibase:grammaticalFeature wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPerfectForm .
    ?presentPerfectForm ontolex:representation ?presentPerfect ;
      wikibase:grammaticalFeature wd:Q1240211 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularCombinedPastParticipleForm .
    ?definiteSingularCombinedPastParticipleForm ontolex:representation ?definiteSingularCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularCombinedPastParticipleForm .
    ?neuterIndefiniteSingularCombinedPastParticipleForm ontolex:representation ?neuterIndefiniteSingularCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786, wd:Q12717679 .
  }
}
