# tool: scribe-data
# All Norwegian (Q25167) personal_pronouns (Q468801) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?personalpronoun
  ?nominative
  ?genitive
  ?neuter
  ?nominativeFeminine
  ?nominativeMasculine

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q468801 ;
    wikibase:lemma ?personalpronoun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?personalpronoun) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterForm .
    ?neuterForm ontolex:representation ?neuter ;
      wikibase:grammaticalFeature wd:Q1775461 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeFeminineForm .
    ?nominativeFeminineForm ontolex:representation ?nominativeFeminine ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeMasculineForm .
    ?nominativeMasculineForm ontolex:representation ?nominativeMasculine ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q499327 .
  }
}
