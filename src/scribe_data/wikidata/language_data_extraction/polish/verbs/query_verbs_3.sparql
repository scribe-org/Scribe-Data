# tool: scribe-data
# All Polish (Q809) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperativeSecondPersonSingular
  ?imperativeSecondPersonPlural
  ?presentSecondPersonSingular
  ?presentSecondPersonPlural
  ?presentThirdPersonSingular
  ?presentThirdPersonPlural

WHERE {
  ?lexeme dct:language wd:Q809 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonSingularForm .
    ?imperativeSecondPersonSingularForm ontolex:representation ?imperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonPluralForm .
    ?imperativeSecondPersonPluralForm ontolex:representation ?imperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentSecondPersonSingularForm .
    ?presentSecondPersonSingularForm ontolex:representation ?presentSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentSecondPersonPluralForm .
    ?presentSecondPersonPluralForm ontolex:representation ?presentSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentThirdPersonSingularForm .
    ?presentThirdPersonSingularForm ontolex:representation ?presentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentThirdPersonPluralForm .
    ?presentThirdPersonPluralForm ontolex:representation ?presentThirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q51929074, wd:Q146786 .
  }
}
