# tool: scribe-data
# All Portuguese (Q5146) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?singular
  ?singularPositive
  ?pluralPositive

WHERE {
  ?lexeme dct:language wd:Q5146 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPositiveForm .
    ?singularPositiveForm ontolex:representation ?singularPositive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralPositiveForm .
    ?pluralPositiveForm ontolex:representation ?pluralPositive ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q3482678 .
  }
}
