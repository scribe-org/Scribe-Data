# tool: scribe-data
# All Italian (Q652) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?singular
  ?plural
  ?superlative
  ?singularSuperlative
  ?pluralSuperlative

WHERE {
  ?lexeme dct:language wd:Q652 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeForm .
    ?superlativeForm ontolex:representation ?superlative ;
      wikibase:grammaticalFeature wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularSuperlativeForm .
    ?singularSuperlativeForm ontolex:representation ?singularSuperlative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralSuperlativeForm .
    ?pluralSuperlativeForm ontolex:representation ?pluralSuperlative ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1817208 .
  }
}
