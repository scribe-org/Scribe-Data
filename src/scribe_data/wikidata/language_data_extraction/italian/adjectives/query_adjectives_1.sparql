# tool: scribe-data
# All Italian (Q652) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?pluralFeminine
  ?pluralMasculine
  ?pluralFeminineSuperlative
  ?pluralMasculineSuperlative

WHERE {
  ?lexeme dct:language wd:Q652 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineForm .
    ?pluralFeminineForm ontolex:representation ?pluralFeminine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineForm .
    ?pluralMasculineForm ontolex:representation ?pluralMasculine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineSuperlativeForm .
    ?pluralFeminineSuperlativeForm ontolex:representation ?pluralFeminineSuperlative ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415, wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineSuperlativeForm .
    ?pluralMasculineSuperlativeForm ontolex:representation ?pluralMasculineSuperlative ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327, wd:Q1817208 .
  }
}
