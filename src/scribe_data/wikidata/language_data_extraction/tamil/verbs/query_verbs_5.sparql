# tool: scribe-data
# All Tamil (Q5885) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePastFirstPersonSingular
  ?simplePastSecondPersonSingular
  ?simpleFutureFirstPersonSingular
  ?simpleFutureSecondPersonSingular

WHERE {
  ?lexeme dct:language wd:Q5885 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastFirstPersonSingularForm .
    ?simplePastFirstPersonSingularForm ontolex:representation ?simplePastFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastSecondPersonSingularForm .
    ?simplePastSecondPersonSingularForm ontolex:representation ?simplePastSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simpleFutureFirstPersonSingularForm .
    ?simpleFutureFirstPersonSingularForm ontolex:representation ?simpleFutureFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q1475560, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simpleFutureSecondPersonSingularForm .
    ?simpleFutureSecondPersonSingularForm ontolex:representation ?simpleFutureSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q1475560, wd:Q51929049, wd:Q110786 .
  }
}
