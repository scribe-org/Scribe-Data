# tool: scribe-data
# All Tamil (Q5885) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePresentFirstPersonSingular
  ?simplePresentFirstPersonPlural
  ?simplePresentSecondPersonSingular
  ?simplePresentSecondPersonPlural
  ?simplePresentThirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q5885 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentFirstPersonSingularForm .
    ?simplePresentFirstPersonSingularForm ontolex:representation ?simplePresentFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentFirstPersonPluralForm .
    ?simplePresentFirstPersonPluralForm ontolex:representation ?simplePresentFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentSecondPersonSingularForm .
    ?simplePresentSecondPersonSingularForm ontolex:representation ?simplePresentSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentSecondPersonPluralForm .
    ?simplePresentSecondPersonPluralForm ontolex:representation ?simplePresentSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentThirdPersonSingularForm .
    ?simplePresentThirdPersonSingularForm ontolex:representation ?simplePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929074, wd:Q110786 .
  }
}
