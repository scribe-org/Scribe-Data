# tool: scribe-data
# All Tamil (Q5885) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?singularNominative
  ?nominativePlural
  ?singularLocative
  ?pluralLocative

WHERE {
  ?lexeme dct:language wd:Q5885 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularNominativeForm .
    ?singularNominativeForm ontolex:representation ?singularNominative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularLocativeForm .
    ?singularLocativeForm ontolex:representation ?singularLocative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q202142 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralLocativeForm .
    ?pluralLocativeForm ontolex:representation ?pluralLocative ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q202142 .
  }
}
