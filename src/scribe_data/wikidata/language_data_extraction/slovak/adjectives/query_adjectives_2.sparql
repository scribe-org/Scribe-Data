# tool: scribe-data
# All Slovak (Q9058) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?genitivePluralPositive
  ?genitiveFeminineSingularPositive
  ?genitiveMasculineSingularPositive
  ?genitiveNeuterSingularPositive

WHERE {
  ?lexeme dct:language wd:Q9058;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralPositiveForm .
    ?genitivePluralPositiveForm ontolex:representation ?genitivePluralPositive ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveFeminineSingularPositiveForm .
    ?genitiveFeminineSingularPositiveForm ontolex:representation ?genitiveFeminineSingularPositive ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q1775415, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculineSingularPositiveForm .
    ?genitiveMasculineSingularPositiveForm ontolex:representation ?genitiveMasculineSingularPositive ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveNeuterSingularPositiveForm .
    ?genitiveNeuterSingularPositiveForm ontolex:representation ?genitiveNeuterSingularPositive ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q1775461, wd:Q110786, wd:Q3482678 .
  }
}
