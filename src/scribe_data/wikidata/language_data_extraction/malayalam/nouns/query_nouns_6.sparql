# tool: scribe-data
# All Malayalam (Q36236) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominativeSingular
  ?nominativePlural
  ?nominativeFemininePlural
  ?nominativeCommonPlural

WHERE {
  ?lexeme dct:language wd:Q36236 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeFemininePluralForm .
    ?nominativeFemininePluralForm ontolex:representation ?nominativeFemininePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeCommonPluralForm .
    ?nominativeCommonPluralForm ontolex:representation ?nominativeCommonPlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q1305037, wd:Q146786 .
  }
}
