# tool: scribe-data
# All Latin (Q397) prepositions (Q4833830) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?preposition
  ?accusative
  ?ablative

WHERE {
  ?lexeme dct:language wd:Q397 ;
    wikibase:lexicalCategory wd:Q4833830 ;
    wikibase:lemma ?preposition ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeForm .
    ?accusativeForm ontolex:representation ?accusative ;
      wikibase:grammaticalFeature wd:Q146078 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativeForm .
    ?ablativeForm ontolex:representation ?ablative ;
      wikibase:grammaticalFeature wd:Q156986 .
  }
}
