# tool: scribe-data
# All Latin (Q397) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?dativeSingular
  ?dativePlural
  ?ablativePlural
  ?ablativeFeminineSingular
  ?ablativeMasculineSingular
  ?ablativeNeuterSingular

WHERE {
  ?lexeme dct:language wd:Q397 ;
      wikibase:lexicalCategory wd:Q36224 ;
      wikibase:lemma ?pronoun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeSingularForm .
    ?dativeSingularForm ontolex:representation ?dativeSingular ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativePluralForm .
    ?dativePluralForm ontolex:representation ?dativePlural ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativePluralForm .
    ?ablativePluralForm ontolex:representation ?ablativePlural ;
      wikibase:grammaticalFeature wd:Q156986, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativeFeminineSingularForm .
    ?ablativeFeminineSingularForm ontolex:representation ?ablativeFeminineSingular ;
      wikibase:grammaticalFeature wd:Q156986, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativeMasculineSingularForm .
    ?ablativeMasculineSingularForm ontolex:representation ?ablativeMasculineSingular ;
      wikibase:grammaticalFeature wd:Q156986, wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?ablativeNeuterSingularForm .
    ?ablativeNeuterSingularForm ontolex:representation ?ablativeNeuterSingular ;
      wikibase:grammaticalFeature wd:Q156986, wd:Q1775461, wd:Q110786 .
  }
}
