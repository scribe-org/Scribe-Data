# tool: scribe-data
# All Arabic (Q13955) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominativeSingular
  ?nominativePlural
  ?nominativeDual
  ?nominativeSingularConstruct
  ?nominativePluralConstruct
  ?nominativeDualConstruct

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeDualForm .
    ?nominativeDualForm ontolex:representation ?nominativeDual ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularConstructForm .
    ?nominativeSingularConstructForm ontolex:representation ?nominativeSingularConstruct ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralConstructForm .
    ?nominativePluralConstructForm ontolex:representation ?nominativePluralConstruct ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeDualConstructForm .
    ?nominativeDualConstructForm ontolex:representation ?nominativeDualConstruct ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110022, wd:Q1641446 .
  }
}
