# tool: scribe-data
# All Arabic (Q13955) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?plural
  ?accusativeSingularConstruct
  ?pausalSingularConstruct
  ?pausalPluralConstruct

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularConstructForm .
    ?accusativeSingularConstructForm ontolex:representation ?accusativeSingularConstruct ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pausalSingularConstructForm .
    ?pausalSingularConstructForm ontolex:representation ?pausalSingularConstruct ;
      wikibase:grammaticalFeature wd:Q117262361, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pausalPluralConstructForm .
    ?pausalPluralConstructForm ontolex:representation ?pausalPluralConstruct ;
      wikibase:grammaticalFeature wd:Q117262361, wd:Q146786, wd:Q1641446 .
  }
}
