# tool: scribe-data
# All Arabic (Q13955) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?nominativeDefiniteSingular
  ?nominativeDefinitePlural
  ?nominativeDefiniteDual
  ?feminineIndefiniteSingular
  ?feminineIndefinitePlural
  ?masculineIndefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeDefiniteSingularForm .
    ?nominativeDefiniteSingularForm ontolex:representation ?nominativeDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeDefinitePluralForm .
    ?nominativeDefinitePluralForm ontolex:representation ?nominativeDefinitePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q53997851, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeDefiniteDualForm .
    ?nominativeDefiniteDualForm ontolex:representation ?nominativeDefiniteDual ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q53997851, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineIndefiniteSingularForm .
    ?feminineIndefiniteSingularForm ontolex:representation ?feminineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineIndefinitePluralForm .
    ?feminineIndefinitePluralForm ontolex:representation ?feminineIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineIndefiniteSingularForm .
    ?masculineIndefiniteSingularForm ontolex:representation ?masculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q53997857, wd:Q110786 .
  }
}
