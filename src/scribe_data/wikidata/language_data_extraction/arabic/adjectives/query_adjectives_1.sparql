# tool: scribe-data
# All Arabic (Q13955) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?pluralFeminine
  ?pluralMasculine
  ?singularMasculineIndefinitePausal

WHERE {
  ?lexeme dct:language wd:Q13955 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineForm .
    ?pluralFeminineForm ontolex:representation ?pluralFeminine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineForm .
    ?pluralMasculineForm ontolex:representation ?pluralMasculine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineIndefinitePausalForm .
    ?singularMasculineIndefinitePausalForm ontolex:representation ?singularMasculineIndefinitePausal ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327, wd:Q53997857, wd:Q117262361 .
  }
}
