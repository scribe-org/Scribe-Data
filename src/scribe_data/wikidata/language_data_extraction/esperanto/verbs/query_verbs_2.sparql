# tool: scribe-data
# All Esperanto (Q143) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?volitive
  ?imperative
  ?present
  ?future
  ?futureCombinedPresentParticiple

WHERE {
  ?lexeme dct:language wd:Q143 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?volitiveForm .
    ?volitiveForm ontolex:representation ?volitive ;
      wikibase:grammaticalFeature wd:Q2532941 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
      wikibase:grammaticalFeature wd:Q501405 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureCombinedPresentParticipleForm .
    ?futureCombinedPresentParticipleForm ontolex:representation ?futureCombinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q10345583 .
  }
}
