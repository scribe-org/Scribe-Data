# tool: scribe-data
# All Esperanto (Q143) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?volitive
  ?conditional
  ?indicativePresent
  ?indicativePast
  ?indicativeFuture

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q143 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Volitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?volitiveForm .
    ?volitiveForm ontolex:representation ?volitive ;
      wikibase:grammaticalFeature wd:Q2532941 .
      FILTER(LANG(?volitive) = "eo")
  }

  # MARK: Conditional

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalForm .
    ?conditionalForm ontolex:representation ?conditional ;
      wikibase:grammaticalFeature wd:Q625581 .
      FILTER(LANG(?conditional) = "eo")
  }

  # MARK: Present Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativePresentForm .
    ?indicativePresentForm ontolex:representation ?indicativePresent ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q682111 .
      FILTER(LANG(?indicativePresent) = "eo")
  }

  # MARK: Past Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativePastForm .
    ?indicativePastForm ontolex:representation ?indicativePast ;
      wikibase:grammaticalFeature wd:Q1994301, wd:Q682111 .
      FILTER(LANG(?indicativePast) = "eo")
  }

  # MARK: Future Tense

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeFutureForm .
    ?indicativeFutureForm ontolex:representation ?indicativeFuture ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q682111 .
      FILTER(LANG(?indicativeFuture) = "eo")
  }
}
