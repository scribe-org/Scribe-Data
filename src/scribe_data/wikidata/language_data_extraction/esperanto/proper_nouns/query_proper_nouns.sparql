# tool: scribe-data
# All Esperanto (Q143) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?singularNominative
  ?nominativePlural
  ?singularAccusative
  ?accusativePlural

WHERE {
  ?lexeme dct:language wd:Q143 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularNominativeForm .
    ?singularNominativeForm ontolex:representation ?singularNominative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularAccusativeForm .
    ?singularAccusativeForm ontolex:representation ?singularAccusative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146078 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }
}
