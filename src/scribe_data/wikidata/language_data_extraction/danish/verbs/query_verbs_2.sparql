# tool: scribe-data
# All Danish (Q9035) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitivePassive
  ?preteritePassive
  ?infinitiveActive
  ?preteriteActive

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitivePassiveForm .
    ?infinitivePassiveForm ontolex:representation ?infinitivePassive ;
      wikibase:grammaticalFeature wd:Q179230, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteritePassiveForm .
    ?preteritePassiveForm ontolex:representation ?preteritePassive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveActiveForm .
    ?infinitiveActiveForm ontolex:representation ?infinitiveActive ;
      wikibase:grammaticalFeature wd:Q179230, wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteActiveForm .
    ?preteriteActiveForm ontolex:representation ?preteriteActive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q1317831 .
  }
}
