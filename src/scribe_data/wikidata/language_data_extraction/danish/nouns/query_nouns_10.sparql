# tool: scribe-data
# All Danish (Q9035) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nonGenitiveNeuterIndefiniteSingular
  ?nonGenitiveNeuterIndefinitePlural
  ?nonGenitiveNeuterDefiniteSingular
  ?nonGenitiveNeuterDefinitePlural

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveNeuterIndefiniteSingularForm .
    ?nonGenitiveNeuterIndefiniteSingularForm ontolex:representation ?nonGenitiveNeuterIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q1775461, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveNeuterIndefinitePluralForm .
    ?nonGenitiveNeuterIndefinitePluralForm ontolex:representation ?nonGenitiveNeuterIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q1775461, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveNeuterDefiniteSingularForm .
    ?nonGenitiveNeuterDefiniteSingularForm ontolex:representation ?nonGenitiveNeuterDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q1775461, wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveNeuterDefinitePluralForm .
    ?nonGenitiveNeuterDefinitePluralForm ontolex:representation ?nonGenitiveNeuterDefinitePlural ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q1775461, wd:Q53997851, wd:Q146786 .
  }
}
