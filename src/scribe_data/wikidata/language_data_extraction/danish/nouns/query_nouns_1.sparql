# tool: scribe-data
# All Danish (Q9035) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?genitivePluralIndefinite
  ?genitivePluralDefinite
  ?pluralIndefiniteNonGenitive
  ?pluralDefiniteNonGenitive

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralIndefiniteForm .
    ?genitivePluralIndefiniteForm ontolex:representation ?genitivePluralIndefinite ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralDefiniteForm .
    ?genitivePluralDefiniteForm ontolex:representation ?genitivePluralDefinite ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralIndefiniteNonGenitiveForm .
    ?pluralIndefiniteNonGenitiveForm ontolex:representation ?pluralIndefiniteNonGenitive ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997857, wd:Q98946930 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralDefiniteNonGenitiveForm .
    ?pluralDefiniteNonGenitiveForm ontolex:representation ?pluralDefiniteNonGenitive ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q53997851, wd:Q98946930 .
  }
}
