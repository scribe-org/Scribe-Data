# tool: scribe-data
# All Danish (Q9035) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nonGenitive
  ?nonGenitiveDefinite
  ?commonDefiniteSingular
  ?neuterIndefiniteSingular
  ?neuterDefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q9035 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveForm .
    ?nonGenitiveForm ontolex:representation ?nonGenitive ;
      wikibase:grammaticalFeature wd:Q98946930 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveDefiniteForm .
    ?nonGenitiveDefiniteForm ontolex:representation ?nonGenitiveDefinite ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?commonDefiniteSingularForm .
    ?commonDefiniteSingularForm ontolex:representation ?commonDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1305037, wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularForm .
    ?neuterIndefiniteSingularForm ontolex:representation ?neuterIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterDefiniteSingularForm .
    ?neuterDefiniteSingularForm ontolex:representation ?neuterDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997851, wd:Q110786 .
  }
}
