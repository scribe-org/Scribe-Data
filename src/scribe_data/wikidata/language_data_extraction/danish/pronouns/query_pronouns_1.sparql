# tool: scribe-data
# All Danish (Q9035) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?genitive
  ?nonGenitive
  ?plural
  ?genitivePlural
  ?pluralNonGenitive

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q36224 ;
    wikibase:lemma ?pronoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveForm .
    ?nonGenitiveForm ontolex:representation ?nonGenitive ;
      wikibase:grammaticalFeature wd:Q98946930 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralNonGenitiveForm .
    ?pluralNonGenitiveForm ontolex:representation ?pluralNonGenitive ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q98946930 .
  }
}
