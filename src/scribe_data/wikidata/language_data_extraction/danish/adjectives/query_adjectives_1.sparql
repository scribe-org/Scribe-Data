# tool: scribe-data
# All Danish (Q9035) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?comparative
  ?superlativeIndefinite
  ?superlativeDefinite
  ?pluralPositive

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeIndefiniteForm .
    ?superlativeIndefiniteForm ontolex:representation ?superlativeIndefinite ;
      wikibase:grammaticalFeature wd:Q1817208, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeDefiniteForm .
    ?superlativeDefiniteForm ontolex:representation ?superlativeDefinite ;
      wikibase:grammaticalFeature wd:Q1817208, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralPositiveForm .
    ?pluralPositiveForm ontolex:representation ?pluralPositive ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q3482678 .
  }
}
