# tool: scribe-data
# All Hindustani (Q11051) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?direct
  ?singular
  ?directSingular
  ?directPlural
  ?directFeminineSingular
  ?directFemininePlural

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for hi to remove hindi (hi) words.
  FILTER(lang(?propernoun) = "hi")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directForm .
    ?directForm ontolex:representation ?direct ;
      wikibase:grammaticalFeature wd:Q1751855 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directSingularForm .
    ?directSingularForm ontolex:representation ?directSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directPluralForm .
    ?directPluralForm ontolex:representation ?directPlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directFeminineSingularForm .
    ?directFeminineSingularForm ontolex:representation ?directFeminineSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directFemininePluralForm .
    ?directFemininePluralForm ontolex:representation ?directFemininePlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q1775415, wd:Q146786 .
  }
}
