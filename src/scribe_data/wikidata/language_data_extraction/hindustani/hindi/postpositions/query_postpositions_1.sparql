# tool: scribe-data
# All Hindustani (Q11051) postpositions (Q161873) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?postposition
  ?vocativeFeminineSingular
  ?vocativeFemininePlural
  ?vocativeMasculineSingular
  ?vocativeMasculinePlural

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q161873 ;
    wikibase:lemma ?postposition ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for hi to remove hindi (hi) words.
  FILTER(lang(?postposition) = "hi")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeFeminineSingularForm .
    ?vocativeFeminineSingularForm ontolex:representation ?vocativeFeminineSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeFemininePluralForm .
    ?vocativeFemininePluralForm ontolex:representation ?vocativeFemininePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeMasculineSingularForm .
    ?vocativeMasculineSingularForm ontolex:representation ?vocativeMasculineSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeMasculinePluralForm .
    ?vocativeMasculinePluralForm ontolex:representation ?vocativeMasculinePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q499327, wd:Q146786 .
  }
}
