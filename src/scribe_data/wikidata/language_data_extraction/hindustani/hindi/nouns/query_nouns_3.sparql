# tool: scribe-data
# All Hindustani (Q11051) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?directSingular
  ?directPlural
  ?directFeminineSingular
  ?directFemininePlural
  ?directMasculineSingular
  ?directMasculinePlural

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for hi to remove hindi (hi) words.
  FILTER(lang(?noun) = "hi")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directSingularForm .
    ?directSingularForm ontolex:representation ?directSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directPluralForm .
    ?directPluralForm ontolex:representation ?directPlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directFeminineSingularForm .
    ?directFeminineSingularForm ontolex:representation ?directFeminineSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directFemininePluralForm .
    ?directFemininePluralForm ontolex:representation ?directFemininePlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directMasculineSingularForm .
    ?directMasculineSingularForm ontolex:representation ?directMasculineSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directMasculinePluralForm .
    ?directMasculinePluralForm ontolex:representation ?directMasculinePlural ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q499327, wd:Q146786 .
  }
}
