# tool: scribe-data
# All Hindustani (Q11051) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?directGerund
  ?secondPersonSingularSubjunctive
  ?secondPersonPluralSubjunctive
  ?directIntransitivePhaseBasicPhaseGerund

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for hi to remove hindi (hi) words.
  FILTER(lang(?verb) = "hi")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directGerundForm .
    ?directGerundForm ontolex:representation ?directGerund ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q1923028 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingularSubjunctiveForm .
    ?secondPersonSingularSubjunctiveForm ontolex:representation ?secondPersonSingularSubjunctive ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralSubjunctiveForm .
    ?secondPersonPluralSubjunctiveForm ontolex:representation ?secondPersonPluralSubjunctive ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directIntransitivePhaseBasicPhaseGerundForm .
    ?directIntransitivePhaseBasicPhaseGerundForm ontolex:representation ?directIntransitivePhaseBasicPhaseGerund ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q113330736, wd:Q113330960, wd:Q1923028 .
  }
}
