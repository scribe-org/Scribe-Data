# tool: scribe-data
# All Hindustani (Q11051) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?feminine
  ?masculine
  ?comparative
  ?superlative
  ?positive
  ?directSingular

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  # Note: We need to filter for ur to remove urdu (ur) words.
  FILTER(lang(?adjective) = "ur")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineForm .
    ?feminineForm ontolex:representation ?feminine ;
      wikibase:grammaticalFeature wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineForm .
    ?masculineForm ontolex:representation ?masculine ;
      wikibase:grammaticalFeature wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeForm .
    ?superlativeForm ontolex:representation ?superlative ;
      wikibase:grammaticalFeature wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?positiveForm .
    ?positiveForm ontolex:representation ?positive ;
      wikibase:grammaticalFeature wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?directSingularForm .
    ?directSingularForm ontolex:representation ?directSingular ;
      wikibase:grammaticalFeature wd:Q1751855, wd:Q110786 .
  }
}
