# tool: scribe-data
# All German (Q188) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?genitive
  ?dative
  ?nominativeAccusative

WHERE {
  ?lexeme dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeForm .
    ?dativeForm ontolex:representation ?dative ;
      wikibase:grammaticalFeature wd:Q145599 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeAccusativeForm .
    ?nominativeAccusativeForm ontolex:representation ?nominativeAccusative ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146078 .
  }
}
