# tool: scribe-data
# All German (Q188) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?dative
  ?dativePlural

WHERE {
  ?lexeme dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeForm .
    ?dativeForm ontolex:representation ?dative ;
      wikibase:grammaticalFeature wd:Q145599 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativePluralForm .
    ?dativePluralForm ontolex:representation ?dativePlural ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q146786 .
  }
}
