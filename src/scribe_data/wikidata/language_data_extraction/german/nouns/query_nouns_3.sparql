# tool: scribe-data
# All German (Q188) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominative
  ?nominativeSingular
  ?nominativePlural
  ?nominativeGenitiveAccusative

WHERE {
  ?lexeme dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeGenitiveAccusativeForm .
    ?nominativeGenitiveAccusativeForm ontolex:representation ?nominativeGenitiveAccusative ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146233, wd:Q146078 .
  }
}
