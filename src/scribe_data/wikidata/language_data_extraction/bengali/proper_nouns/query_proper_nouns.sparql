# tool: scribe-data
# All Bengali (Q9610) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nominative
  ?genitive
  ?accusative
  ?locative

WHERE {
  ?lexeme dct:language wd:Q9610 ;
    wikibase:lexicalCategory wd:Q147276 ;

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  # MARK: Accusative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeForm .
    ?accusativeForm ontolex:representation ?accusative ;
      wikibase:grammaticalFeature wd:Q146078 .
  }

  # MARK: Locative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeForm .
    ?locativeForm ontolex:representation ?locative ;
      wikibase:grammaticalFeature wd:Q202142 .
  }
}
