# tool: scribe-data
# All Bengali (Q9610) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominative
  ?genitive
  ?dative
  ?accusative
  ?locative

WHERE {
  ?lexeme dct:language wd:Q9610 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeForm .
    ?dativeForm ontolex:representation ?dative ;
      wikibase:grammaticalFeature wd:Q145599 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeForm .
    ?accusativeForm ontolex:representation ?accusative ;
      wikibase:grammaticalFeature wd:Q146078 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeForm .
    ?locativeForm ontolex:representation ?locative ;
      wikibase:grammaticalFeature wd:Q202142 .
  }
}
