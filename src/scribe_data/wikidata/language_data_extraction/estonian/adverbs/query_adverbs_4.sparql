# tool: scribe-data
# All Estonian (Q9072) adverbs (Q380057) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adverb
  ?singularGenitive
  ?singularPartitive
  ?singularTerminative
  ?singularEssive
  ?singularAbessive
  ?singularComitative

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q380057 ;
    wikibase:lemma ?adverb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularGenitiveForm .
    ?singularGenitiveForm ontolex:representation ?singularGenitive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPartitiveForm .
    ?singularPartitiveForm ontolex:representation ?singularPartitive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q857325 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularTerminativeForm .
    ?singularTerminativeForm ontolex:representation ?singularTerminative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q747019 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularEssiveForm .
    ?singularEssiveForm ontolex:representation ?singularEssive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q148465 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularAbessiveForm .
    ?singularAbessiveForm ontolex:representation ?singularAbessive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q319822 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularComitativeForm .
    ?singularComitativeForm ontolex:representation ?singularComitative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q838581 .
  }
}
