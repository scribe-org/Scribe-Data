# tool: scribe-data
# All Estonian (Q9072) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?genitiveSingular
  ?genitivePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }
}
