# tool: scribe-data
# All Estonian (Q9072) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?nominativePlural
  ?genitivePlural
  ?singularElative
  ?singularAllative

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularElativeForm .
    ?singularElativeForm ontolex:representation ?singularElative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q394253 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularAllativeForm .
    ?singularAllativeForm ontolex:representation ?singularAllative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q655020 .
  }
}
