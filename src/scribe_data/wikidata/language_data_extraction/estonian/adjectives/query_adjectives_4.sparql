# tool: scribe-data
# All Estonian (Q9072) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?essiveSingular
  ?essivePlural
  ?abessiveSingular
  ?abessivePlural
  ?comitativeSingular
  ?comitativePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Essive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?essiveSingularForm .
    ?essiveSingularForm ontolex:representation ?essiveSingular ;
      wikibase:grammaticalFeature wd:Q148465, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?essivePluralForm .
    ?essivePluralForm ontolex:representation ?essivePlural ;
      wikibase:grammaticalFeature wd:Q148465, wd:Q146786 .
  }

  # MARK: Abessive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?abessiveSingularForm .
    ?abessiveSingularForm ontolex:representation ?abessiveSingular ;
      wikibase:grammaticalFeature wd:Q319822, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?abessivePluralForm .
    ?abessivePluralForm ontolex:representation ?abessivePlural ;
      wikibase:grammaticalFeature wd:Q319822, wd:Q146786 .
  }

  # MARK: Comitative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativeSingularForm .
    ?comitativeSingularForm ontolex:representation ?comitativeSingular ;
      wikibase:grammaticalFeature wd:Q838581, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativePluralForm .
    ?comitativePluralForm ontolex:representation ?comitativePlural ;
      wikibase:grammaticalFeature wd:Q838581, wd:Q146786 .
  }
}
