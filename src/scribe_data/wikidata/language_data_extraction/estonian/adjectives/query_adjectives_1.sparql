# tool: scribe-data
# All Estonian (Q9072) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?nominativeSingular
  ?nominativePlural
  ?genitiveSingular
  ?genitivePlural
  ?partitiveSingular
  ?partitivePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  # MARK: Partitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitiveSingularForm .
    ?partitiveSingularForm ontolex:representation ?partitiveSingular ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitivePluralForm .
    ?partitivePluralForm ontolex:representation ?partitivePlural ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q146786 .
  }
}
