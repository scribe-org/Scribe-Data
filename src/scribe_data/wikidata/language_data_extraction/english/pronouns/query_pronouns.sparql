# tool: scribe-data
# All english (Q1860) pronouns (Q36224) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?pronouns
    ?plural
  ?singular
  ?nominative
  ?oblique

WHERE {
  ?lexeme dct:language wd:Q1860 ;
  wikibase:lexicalCategory wd:Q36224 ;
  wikibase:lemma ?pronouns .
  FILTER(lang(?pronouns) = "en")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueForm .
    ?obliqueForm ontolex:representation ?oblique ;
      wikibase:grammaticalFeature wd:Q1233197 .
  }
}
