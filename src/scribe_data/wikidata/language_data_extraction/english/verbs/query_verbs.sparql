# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?infinitive
  ?simplePresent
  ?simplePast
  ?presentAndParticiple
  ?pastParticipleEnglish
  ?simplePresentThirdPersonSingular

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q1860 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive ;
    schema:dateModified ?lastModified .

  # MARK: Simple Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentForm .
    ?simplePresentForm ontolex:representation ?simplePresent ;
      wikibase:grammaticalFeature wd:Q3910936 .
      FILTER NOT EXISTS { ?simplePresentForm wikibase:grammaticalFeature wd:Q51929074 . }
      FILTER NOT EXISTS { ?simplePresentForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePresentForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePresent) = "en")
  }

  # MARK: Simple Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastForm .
    ?simplePastForm ontolex:representation ?simplePast ;
      wikibase:grammaticalFeature wd:Q1392475 .
      FILTER NOT EXISTS { ?simplePastForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePastForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePast) = "en")
  }

  # MARK: Present Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentAndParticipleForm .
    ?presentAndParticipleForm ontolex:representation ?presentAndParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
      FILTER NOT EXISTS { ?presentAndParticipleForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?presentAndParticipleForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?presentAndParticiple) = "en")
  }

  # MARK: Past Participle English

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleEnglishForm .
    ?pastParticipleEnglishForm ontolex:representation ?pastParticipleEnglish ;
      wikibase:grammaticalFeature wd:Q1230649 .
      FILTER NOT EXISTS { ?pastParticipleEnglishForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?pastParticipleEnglishForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?pastParticipleEnglish) = "en")
  }

  # MARK: Third-person Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentThirdPersonSingularForm .
    ?simplePresentThirdPersonSingularForm ontolex:representation ?simplePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929074, wd:Q110786 .
      FILTER NOT EXISTS { ?simplePresentThirdPersonSingularForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePresentThirdPersonSingularForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePresentThirdPersonSingular) = "en")
  }
}
