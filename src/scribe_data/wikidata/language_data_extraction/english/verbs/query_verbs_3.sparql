# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?past
  ?combinedPresentParticiple
  ?combinedPastParticiple
  ?englishPastParticiple
  ?contraction
  ?thirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPastParticipleForm .
    ?combinedPastParticipleForm ontolex:representation ?combinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?englishPastParticipleForm .
    ?englishPastParticipleForm ontolex:representation ?englishPastParticiple ;
      wikibase:grammaticalFeature wd:Q1230649 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?contractionForm .
    ?contractionForm ontolex:representation ?contraction ;
      wikibase:grammaticalFeature wd:Q126473 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingularForm .
    ?thirdPersonSingularForm ontolex:representation ?thirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786 .
  }
}
