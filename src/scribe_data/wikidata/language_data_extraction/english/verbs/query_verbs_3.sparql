# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperative
  ?past
  ?plural
  ?combinedPastParticiple
  ?englishPastParticiple
  ?preteriteSubjunctive

WHERE {
  ?lexeme dct:language wd:Q1860 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPastParticipleForm .
    ?combinedPastParticipleForm ontolex:representation ?combinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?englishPastParticipleForm .
    ?englishPastParticipleForm ontolex:representation ?englishPastParticiple ;
      wikibase:grammaticalFeature wd:Q1230649 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteSubjunctiveForm .
    ?preteriteSubjunctiveForm ontolex:representation ?preteriteSubjunctive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q473746 .
  }
}
