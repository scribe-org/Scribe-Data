# tool: scribe-data
# All Swedish (Q9027) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?preteritePassive
  ?supinePassive
  ?preteriteActive
  ?supineActive

WHERE {
  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteritePassiveForm .
    ?preteritePassiveForm ontolex:representation ?preteritePassive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?supinePassiveForm .
    ?supinePassiveForm ontolex:representation ?supinePassive ;
      wikibase:grammaticalFeature wd:Q548470, wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteActiveForm .
    ?preteriteActiveForm ontolex:representation ?preteriteActive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?supineActiveForm .
    ?supineActiveForm ontolex:representation ?supineActive ;
      wikibase:grammaticalFeature wd:Q548470, wd:Q1317831 .
  }
}
