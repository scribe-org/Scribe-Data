# tool: scribe-data
# All Swedish (Q9027) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?activeImperative
  ?activeInfinitive
  ?activeSupine
  ?futureSubjunctive
  ?activePastPreterite
  ?activePluperfectSupine

WHERE {
  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeImperativeForm .
    ?activeImperativeForm ontolex:representation ?activeImperative ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeInfinitiveForm .
    ?activeInfinitiveForm ontolex:representation ?activeInfinitive ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeSupineForm .
    ?activeSupineForm ontolex:representation ?activeSupine ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q548470 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureSubjunctiveForm .
    ?futureSubjunctiveForm ontolex:representation ?futureSubjunctive ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePastPreteriteForm .
    ?activePastPreteriteForm ontolex:representation ?activePastPreterite ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q1994301, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePluperfectSupineForm .
    ?activePluperfectSupineForm ontolex:representation ?activePluperfectSupine ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q623742, wd:Q548470 .
  }
}
