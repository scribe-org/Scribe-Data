# tool: scribe-data
# All Swedish (Q9027) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?present
  ?past
  ?preterite
  ?supine
  ?pastParticiple
  ?subjunctive
  ?activeImperative
  ?futureSubjunctive
  ?passivePreteritePlural
  ?activePresentPlural
  ?activePastPreterite
  ?activePreteritePlural
  ?activePluperfectSupine

WHERE {
  ?lexeme dct:language wd:Q9027 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteForm .
    ?preteriteForm ontolex:representation ?preterite ;
      wikibase:grammaticalFeature wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?supineForm .
    ?supineForm ontolex:representation ?supine ;
      wikibase:grammaticalFeature wd:Q548470 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?subjunctiveForm .
    ?subjunctiveForm ontolex:representation ?subjunctive ;
      wikibase:grammaticalFeature wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeImperativeForm .
    ?activeImperativeForm ontolex:representation ?activeImperative ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureSubjunctiveForm .
    ?futureSubjunctiveForm ontolex:representation ?futureSubjunctive ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePreteritePluralForm .
    ?passivePreteritePluralForm ontolex:representation ?passivePreteritePlural ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q442485, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePresentPluralForm .
    ?activePresentPluralForm ontolex:representation ?activePresentPlural ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q192613, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePastPreteriteForm .
    ?activePastPreteriteForm ontolex:representation ?activePastPreterite ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q1994301, wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePreteritePluralForm .
    ?activePreteritePluralForm ontolex:representation ?activePreteritePlural ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q442485, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePluperfectSupineForm .
    ?activePluperfectSupineForm ontolex:representation ?activePluperfectSupine ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q623742, wd:Q548470 .
  }
}
