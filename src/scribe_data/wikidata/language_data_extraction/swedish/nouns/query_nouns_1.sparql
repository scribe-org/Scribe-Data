# tool: scribe-data
# All Swedish (Q9027) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominativePluralIndefinite
  ?nominativePluralDefinite
  ?genitivePluralIndefinite
  ?genitivePluralDefinite

WHERE {
  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralIndefiniteForm .
    ?nominativePluralIndefiniteForm ontolex:representation ?nominativePluralIndefinite ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralDefiniteForm .
    ?nominativePluralDefiniteForm ontolex:representation ?nominativePluralDefinite ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralIndefiniteForm .
    ?genitivePluralIndefiniteForm ontolex:representation ?genitivePluralIndefinite ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralDefiniteForm .
    ?genitivePluralDefiniteForm ontolex:representation ?genitivePluralDefinite ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q53997851 .
  }
}
