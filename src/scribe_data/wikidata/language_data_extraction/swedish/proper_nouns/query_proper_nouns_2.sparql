# tool: scribe-data
# All Swedish (Q9027) proper_nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?propernoun
  ?nominative
  ?genitive
  ?nominativeSingular
  ?genitiveSingular
  ?nominativeIndefiniteSingular
  ?genitiveIndefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?propernoun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeIndefiniteSingularForm .
    ?nominativeIndefiniteSingularForm ontolex:representation ?nominativeIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveIndefiniteSingularForm .
    ?genitiveIndefiniteSingularForm ontolex:representation ?genitiveIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q53997857, wd:Q110786 .
  }
}
