# tool: scribe-data
# All Swedish (Q9027) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?neuterSingular
  ?neuterIndefiniteSingular
  ?neuterIndefiniteSingularPositive

WHERE {
  ?lexeme dct:language wd:Q9027 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularForm .
    ?neuterSingularForm ontolex:representation ?neuterSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularForm .
    ?neuterIndefiniteSingularForm ontolex:representation ?neuterIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularPositiveForm .
    ?neuterIndefiniteSingularPositiveForm ontolex:representation ?neuterIndefiniteSingularPositive ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786, wd:Q3482678 .
  }
}
