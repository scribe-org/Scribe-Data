# tool: scribe-data
# All Latvian (Q9078) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nominativeSingular
  ?nominativePlural
  ?genitiveSingular
  ?genitivePlural
  ?dativeSingular
  ?dativePlural
  ?accusativeSingular
  ?accusativePlural
  ?instrumentalSingular
  ?instrumentalPlural
  ?locativeSingular
  ?locativePlural
  ?vocativeSingular
  ?vocativePlural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q9078 ;
    wikibase:lexicalCategory wd:Q1084 .

  # MARK: Nominative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  # MARK: Dative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeSingularForm .
    ?dativeSingularForm ontolex:representation ?dativeSingular ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativePluralForm .
    ?dativePluralForm ontolex:representation ?dativePlural ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q146786 .
  }

  # MARK: Accusative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularForm .
    ?accusativeSingularForm ontolex:representation ?accusativeSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }

  # MARK: Instrumental

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalSingularForm .
    ?instrumentalSingularForm ontolex:representation ?instrumentalSingular ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalPluralForm .
    ?instrumentalPluralForm ontolex:representation ?instrumentalPlural ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q146786 .
  }

  # MARK: Locative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeSingularForm .
    ?locativeSingularForm ontolex:representation ?locativeSingular ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativePluralForm .
    ?locativePluralForm ontolex:representation ?locativePlural ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q146786 .
  }

  # MARK: Vocative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeSingularForm .
    ?vocativeSingularForm ontolex:representation ?vocativeSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativePluralForm .
    ?vocativePluralForm ontolex:representation ?vocativePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q146786 .
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
