# tool: scribe-data
# All Czech (Q9056) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?comparative
  ?genitiveSingular
  ?genitivePlural
  ?locativeSingular
  ?locativePlural
  ?vocativePlural

WHERE {
  ?lexeme dct:language wd:Q9056 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeSingularForm .
    ?locativeSingularForm ontolex:representation ?locativeSingular ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativePluralForm .
    ?locativePluralForm ontolex:representation ?locativePlural ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativePluralForm .
    ?vocativePluralForm ontolex:representation ?vocativePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q146786 .
  }
}
