# tool: scribe-data
# All Czech (Q9056) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?genitiveMasculineSingular
  ?genitiveMasculinePlural
  ?locativeFeminineSingular
  ?neuterSingularPositive
  ?neuterPluralPositive
  ?locativeFemininePluralPositive

WHERE {
  ?lexeme dct:language wd:Q9056 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculineSingularForm .
    ?genitiveMasculineSingularForm ontolex:representation ?genitiveMasculineSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculinePluralForm .
    ?genitiveMasculinePluralForm ontolex:representation ?genitiveMasculinePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeFeminineSingularForm .
    ?locativeFeminineSingularForm ontolex:representation ?locativeFeminineSingular ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularPositiveForm .
    ?neuterSingularPositiveForm ontolex:representation ?neuterSingularPositive ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterPluralPositiveForm .
    ?neuterPluralPositiveForm ontolex:representation ?neuterPluralPositive ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q146786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeFemininePluralPositiveForm .
    ?locativeFemininePluralPositiveForm ontolex:representation ?locativeFemininePluralPositive ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q1775415, wd:Q146786, wd:Q3482678 .
  }
}
