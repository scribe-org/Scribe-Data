# tool: scribe-data
# All Czech (Q9056) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?nominativeSingular
  ?nominativePlural
  ?accusativeSingular
  ?accusativePlural
  ?nominativeMasculineSingular
  ?accusativeMasculinePlural

WHERE {
  ?lexeme dct:language wd:Q9056 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularForm .
    ?accusativeSingularForm ontolex:representation ?accusativeSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeMasculineSingularForm .
    ?nominativeMasculineSingularForm ontolex:representation ?nominativeMasculineSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeMasculinePluralForm .
    ?accusativeMasculinePluralForm ontolex:representation ?accusativeMasculinePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q499327, wd:Q146786 .
  }
}
