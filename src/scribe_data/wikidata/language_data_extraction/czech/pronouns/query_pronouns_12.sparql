# tool: scribe-data
# All Czech (Q9056) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?genitive
  ?genitivePluralFeminine
  ?genitivePluralMasculineInanimate
  ?genitivePluralMasculineAnimate
  ?genitivePluralNeuter

WHERE {
  ?lexeme dct:language wd:Q9056 ;
    wikibase:lexicalCategory wd:Q36224 ;
    wikibase:lemma ?pronoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralFeminineForm .
    ?genitivePluralFeminineForm ontolex:representation ?genitivePluralFeminine ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralMasculineInanimateForm .
    ?genitivePluralMasculineInanimateForm ontolex:representation ?genitivePluralMasculineInanimate ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q52943434 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralMasculineAnimateForm .
    ?genitivePluralMasculineAnimateForm ontolex:representation ?genitivePluralMasculineAnimate ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q54020116 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralNeuterForm .
    ?genitivePluralNeuterForm ontolex:representation ?genitivePluralNeuter ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q1775461 .
  }
}
