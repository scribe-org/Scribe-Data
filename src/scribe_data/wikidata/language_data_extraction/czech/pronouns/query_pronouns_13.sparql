# tool: scribe-data
# All Czech (Q9056) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?nominative
  ?nominativePluralFeminine
  ?nominativePluralMasculineInanimate
  ?nominativePluralMasculineAnimate
  ?nominativePluralNeuter

WHERE {
  ?lexeme dct:language wd:Q9056 ;
    wikibase:lexicalCategory wd:Q36224 ;
    wikibase:lemma ?pronoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralFeminineForm .
    ?nominativePluralFeminineForm ontolex:representation ?nominativePluralFeminine ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralMasculineInanimateForm .
    ?nominativePluralMasculineInanimateForm ontolex:representation ?nominativePluralMasculineInanimate ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q52943434 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralMasculineAnimateForm .
    ?nominativePluralMasculineAnimateForm ontolex:representation ?nominativePluralMasculineAnimate ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q54020116 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralNeuterForm .
    ?nominativePluralNeuterForm ontolex:representation ?nominativePluralNeuter ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q1775461 .
  }
}
