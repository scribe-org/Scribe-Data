# tool: scribe-data
# All Czech (Q9056) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?nominative
  ?genitive
  ?genitiveSingular
  ?genitivePlural
  ?nominativeNeuterPlural

WHERE {
  ?lexeme dct:language wd:Q9056 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeForm .
    ?nominativeForm ontolex:representation ?nominative ;
      wikibase:grammaticalFeature wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeNeuterPluralForm .
    ?nominativeNeuterPluralForm ontolex:representation ?nominativeNeuterPlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q1775461, wd:Q146786 .
  }
}
