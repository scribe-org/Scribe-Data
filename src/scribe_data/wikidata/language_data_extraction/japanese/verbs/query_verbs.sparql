# tool: scribe-data
# All Japanese (Q5287) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?negative
  ?conjunctive
  ?attributive
  ?hypothetical
  ?imperfective

WHERE {
  ?lexeme dct:language wd:Q5287 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .
    FILTER(lang(?verb) = "ja-hira")

  # MARK: Negative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?negativeForm .
    ?negativeForm ontolex:representation ?negative ;
      wikibase:grammaticalFeature wd:Q15737187 .
      FILTER(LANG(?negative) = "ja-hira")
  }

  # MARK: Conjunctive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conjunctiveForm .
    ?conjunctiveForm ontolex:representation ?conjunctive ;
      wikibase:grammaticalFeature wd:Q2888577 .
      FILTER(LANG(?conjunctive) = "ja-hira")
  }

  # MARK: Attributive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?attributiveForm .
    ?attributiveForm ontolex:representation ?attributive ;
      wikibase:grammaticalFeature wd:Q53608953 .
      FILTER(LANG(?attributive) = "ja-hira")
  }

  # MARK: Hypothetical

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?hypotheticalForm .
    ?hypotheticalForm ontolex:representation ?hypothetical ;
      wikibase:grammaticalFeature wd:Q53609593 .
      FILTER(LANG(?hypothetical) = "ja-hira")
  }

  # MARK: Imperfective

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperfectiveForm .
    ?imperfectiveForm ontolex:representation ?imperfective ;
      wikibase:grammaticalFeature wd:Q2898727 .
      FILTER(LANG(?imperfective) = "ja-hira")
  }
}
