# tool: scribe-data
# All French (Q150) prepositions (Q4833830) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?preposition
  ?plural
  ?feminineSingular
  ?masculineSingular

WHERE {
  ?lexeme dct:language wd:Q150 ;
    wikibase:lexicalCategory wd:Q4833830 ;
    wikibase:lemma ?preposition ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }
}
