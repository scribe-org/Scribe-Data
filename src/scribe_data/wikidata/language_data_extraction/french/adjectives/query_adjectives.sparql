# tool: scribe-data
# All French (Q150) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?singular
  ?plural
  ?singularFeminine
  ?pluralFeminine
  ?singularMasculine
  ?pluralMasculine

WHERE {
  ?lexeme dct:language wd:Q150 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularFeminineForm .
    ?singularFeminineForm ontolex:representation ?singularFeminine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineForm .
    ?pluralFeminineForm ontolex:representation ?pluralFeminine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineForm .
    ?singularMasculineForm ontolex:representation ?singularMasculine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineForm .
    ?pluralMasculineForm ontolex:representation ?pluralMasculine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327 .
  }
}
