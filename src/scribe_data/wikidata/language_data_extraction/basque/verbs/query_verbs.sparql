# tool: scribe-data
# All Basque (Q8752) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?future
  ?nominalized
  ?participle
  ?gerundImperfective

WHERE {
  ?lexeme dct:language wd:Q8752 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
      wikibase:grammaticalFeature wd:Q501405 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominalizedForm .
    ?nominalizedForm ontolex:representation ?nominalized ;
      wikibase:grammaticalFeature wd:Q74674960 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?participleForm .
    ?participleForm ontolex:representation ?participle ;
      wikibase:grammaticalFeature wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundImperfectiveForm .
    ?gerundImperfectiveForm ontolex:representation ?gerundImperfective ;
      wikibase:grammaticalFeature wd:Q1923028, wd:Q54556033 .
  }
}
