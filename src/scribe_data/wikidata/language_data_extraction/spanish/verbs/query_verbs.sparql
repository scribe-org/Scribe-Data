# tool: scribe-data
# All Spanish (Q1321) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?gerund
  ?participle
  ?feminineSingularParticiple
  ?femininePluralParticiple

WHERE {
  ?lexeme dct:language wd:Q1321 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundForm .
    ?gerundForm ontolex:representation ?gerund ;
      wikibase:grammaticalFeature wd:Q1923028 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?participleForm .
    ?participleForm ontolex:representation ?participle ;
      wikibase:grammaticalFeature wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularParticipleForm .
    ?feminineSingularParticipleForm ontolex:representation ?feminineSingularParticiple ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralParticipleForm .
    ?femininePluralParticipleForm ontolex:representation ?femininePluralParticiple ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q814722 .
  }
}
