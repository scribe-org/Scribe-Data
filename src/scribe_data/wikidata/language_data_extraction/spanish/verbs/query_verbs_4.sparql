# tool: scribe-data
# All Spanish (Q1321) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?masculineSingularParticiple
  ?masculinePluralParticiple
  ?imperativeFirstPersonPlural
  ?imperativeSecondPersonSingular
  ?imperativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q1321 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularParticipleForm .
    ?masculineSingularParticipleForm ontolex:representation ?masculineSingularParticiple ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralParticipleForm .
    ?masculinePluralParticipleForm ontolex:representation ?masculinePluralParticiple ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeFirstPersonPluralForm .
    ?imperativeFirstPersonPluralForm ontolex:representation ?imperativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonSingularForm .
    ?imperativeSecondPersonSingularForm ontolex:representation ?imperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonPluralForm .
    ?imperativeSecondPersonPluralForm ontolex:representation ?imperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q146786 .
  }
}
