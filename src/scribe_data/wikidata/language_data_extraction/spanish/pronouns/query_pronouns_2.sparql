# tool: scribe-data
# All Spanish (Q1321) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?masculineSingular
  ?masculinePlural
  ?masculineNeuterSingular
  ?masculineNeuterPlural

WHERE {
  ?lexeme dct:language wd:Q1321 ;
    wikibase:lexicalCategory wd:Q36224 ;
    wikibase:lemma ?pronoun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineNeuterSingularForm .
    ?masculineNeuterSingularForm ontolex:representation ?masculineNeuterSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q1775461, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineNeuterPluralForm .
    ?masculineNeuterPluralForm ontolex:representation ?masculineNeuterPlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q1775461, wd:Q146786 .
  }
}
