# tool: scribe-data
# All Indonesian (Q9240) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?passive
  ?active
  ?thirdPerson
  ?plural
  ?phrase
  ?comparative

WHERE {
  ?lexeme dct:language wd:Q9240 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveForm .
    ?passiveForm ontolex:representation ?passive ;
      wikibase:grammaticalFeature wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeForm .
    ?activeForm ontolex:representation ?active ;
      wikibase:grammaticalFeature wd:Q1317831 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonForm .
    ?thirdPersonForm ontolex:representation ?thirdPerson ;
      wikibase:grammaticalFeature wd:Q51929074 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?phraseForm .
    ?phraseForm ontolex:representation ?phrase ;
      wikibase:grammaticalFeature wd:Q187931 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }
}
