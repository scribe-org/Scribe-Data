# tool: scribe-data
# All Indonesian (Q9240) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?passive
  ?passiveFirstPerson
  ?passiveSecondPerson

WHERE {
  ?lexeme dct:language wd:Q9240 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveForm .
    ?passiveForm ontolex:representation ?passive ;
      wikibase:grammaticalFeature wd:Q1194697 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveFirstPersonForm .
    ?passiveFirstPersonForm ontolex:representation ?passiveFirstPerson ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q21714344 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveSecondPersonForm .
    ?passiveSecondPersonForm ontolex:representation ?passiveSecondPerson ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q51929049 .
  }
}
