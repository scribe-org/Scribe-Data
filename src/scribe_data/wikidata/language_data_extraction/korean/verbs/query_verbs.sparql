# tool: scribe-data
# All Korean (Q9176) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?present
  ?past
  ?wordStem

WHERE {
  ?lexeme dct:language wd:Q9176 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?wordStemForm .
    ?wordStemForm ontolex:representation ?wordStem ;
      wikibase:grammaticalFeature wd:Q210523 .
  }
}
