# tool: scribe-data
# All Kurmanji (Q36163) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?pluralVocative
  ?pluralDirectIndefinite
  ?pluralDirectDefinite
  ?pluralObliqueIndefinite
  ?pluralObliqueDefinite

WHERE {
  ?lexeme dct:language wd:Q36163 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralVocativeForm .
    ?pluralVocativeForm ontolex:representation ?pluralVocative ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q185077 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralDirectIndefiniteForm .
    ?pluralDirectIndefiniteForm ontolex:representation ?pluralDirectIndefinite ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1751855, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralDirectDefiniteForm .
    ?pluralDirectDefiniteForm ontolex:representation ?pluralDirectDefinite ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1751855, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralObliqueIndefiniteForm .
    ?pluralObliqueIndefiniteForm ontolex:representation ?pluralObliqueIndefinite ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1233197, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralObliqueDefiniteForm .
    ?pluralObliqueDefiniteForm ontolex:representation ?pluralObliqueDefinite ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1233197, wd:Q53997851 .
  }
}
