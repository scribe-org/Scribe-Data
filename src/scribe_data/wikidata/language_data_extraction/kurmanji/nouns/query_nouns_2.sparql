# tool: scribe-data
# All Kurmanji (Q36163) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?vocativeSecondPersonSingular
  ?vocativeSecondPersonPlural
  ?obliqueFeminineSingular
  ?obliqueFemininePlural
  ?obliqueDefiniteSingular
  ?obliqueDefinitePlural

WHERE {
  ?lexeme dct:language wd:Q36163 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeSecondPersonSingularForm .
    ?vocativeSecondPersonSingularForm ontolex:representation ?vocativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeSecondPersonPluralForm .
    ?vocativeSecondPersonPluralForm ontolex:representation ?vocativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueFeminineSingularForm .
    ?obliqueFeminineSingularForm ontolex:representation ?obliqueFeminineSingular ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueFemininePluralForm .
    ?obliqueFemininePluralForm ontolex:representation ?obliqueFemininePlural ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueDefiniteSingularForm .
    ?obliqueDefiniteSingularForm ontolex:representation ?obliqueDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q53997851, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?obliqueDefinitePluralForm .
    ?obliqueDefinitePluralForm ontolex:representation ?obliqueDefinitePlural ;
      wikibase:grammaticalFeature wd:Q1233197, wd:Q53997851, wd:Q146786 .
  }
}
