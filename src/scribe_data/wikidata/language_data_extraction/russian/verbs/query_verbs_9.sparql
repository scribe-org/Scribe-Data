# tool: scribe-data
# All Russian (Q7737) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?presentIndicativeFirstPersonSingular
  ?presentIndicativeFirstPersonPlural
  ?presentIndicativeSecondPersonSingular
  ?presentIndicativeSecondPersonPlural
  ?presentIndicativeThirdPersonSingular
  ?presentIndicativeThirdPersonPlural

WHERE {
  ?lexeme dct:language wd:Q7737 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeFirstPersonSingularForm .
    ?presentIndicativeFirstPersonSingularForm ontolex:representation ?presentIndicativeFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeFirstPersonPluralForm .
    ?presentIndicativeFirstPersonPluralForm ontolex:representation ?presentIndicativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeSecondPersonSingularForm .
    ?presentIndicativeSecondPersonSingularForm ontolex:representation ?presentIndicativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeSecondPersonPluralForm .
    ?presentIndicativeSecondPersonPluralForm ontolex:representation ?presentIndicativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeThirdPersonSingularForm .
    ?presentIndicativeThirdPersonSingularForm ontolex:representation ?presentIndicativeThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeThirdPersonPluralForm .
    ?presentIndicativeThirdPersonPluralForm ontolex:representation ?presentIndicativeThirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q56682909, wd:Q51929074, wd:Q146786 .
  }
}
