# tool: scribe-data
# All Russian (Q7737) personal_pronouns (Q468801) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?personalpronoun
  ?genitive
  ?instrumental

WHERE {
  ?lexeme dct:language wd:Q7737 ;
    wikibase:lexicalCategory wd:Q468801 ;
    wikibase:lemma ?personalpronoun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveForm .
    ?genitiveForm ontolex:representation ?genitive ;
      wikibase:grammaticalFeature wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalForm .
    ?instrumentalForm ontolex:representation ?instrumental ;
      wikibase:grammaticalFeature wd:Q192997 .
  }
}
