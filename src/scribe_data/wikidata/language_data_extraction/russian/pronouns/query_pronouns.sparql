# tool: scribe-data
# All Russian (Q7737) pronouns (Q36224) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?pronoun
  ?instrumental
  ?prepositional
  ?nominativeSingular
  ?instrumentalSingular
  ?prepositionalSingular

WHERE {
  ?lexeme dct:language wd:Q7737 ;
      wikibase:lexicalCategory wd:Q36224 ;
      wikibase:lemma ?pronoun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalForm .
    ?instrumentalForm ontolex:representation ?instrumental ;
      wikibase:grammaticalFeature wd:Q192997 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?prepositionalForm .
    ?prepositionalForm ontolex:representation ?prepositional ;
      wikibase:grammaticalFeature wd:Q2114906 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalSingularForm .
    ?instrumentalSingularForm ontolex:representation ?instrumentalSingular ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?prepositionalSingularForm .
    ?prepositionalSingularForm ontolex:representation ?prepositionalSingular ;
      wikibase:grammaticalFeature wd:Q2114906, wd:Q110786 .
  }
}
