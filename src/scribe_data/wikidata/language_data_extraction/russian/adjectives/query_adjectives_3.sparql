# tool: scribe-data
# All Russian (Q7737) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?plural
  ?masculineSingular
  ?neuterSingular
  ?pluralShort
  ?masculineSingularShort
  ?neuterSingularShort

WHERE {
  ?lexeme dct:language wd:Q7737 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularForm .
    ?neuterSingularForm ontolex:representation ?neuterSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralShortForm .
    ?pluralShortForm ontolex:representation ?pluralShort ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q4239848 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularShortForm .
    ?masculineSingularShortForm ontolex:representation ?masculineSingularShort ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q4239848 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularShortForm .
    ?neuterSingularShortForm ontolex:representation ?neuterSingularShort ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786, wd:Q4239848 .
  }
}
