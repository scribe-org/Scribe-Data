# tool: scribe-data
# All Russian (Q7737) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?plural
  ?comparative
  ?feminineSingular
  ?neuterSingular
  ?accusativeNeuterSingular
  ?accusativeInanimateNeuterSingular

WHERE {
  ?lexeme dct:language wd:Q7737 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSingularForm .
    ?neuterSingularForm ontolex:representation ?neuterSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeNeuterSingularForm .
    ?accusativeNeuterSingularForm ontolex:representation ?accusativeNeuterSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q1775461, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeInanimateNeuterSingularForm .
    ?accusativeInanimateNeuterSingularForm ontolex:representation ?accusativeInanimateNeuterSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q51927539, wd:Q1775461, wd:Q110786 .
  }
}
