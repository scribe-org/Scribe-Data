# tool: scribe-data
# All Greek (Q36510) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?singular
  ?genitiveSingular
  ?genitivePlural
  ?accusativeSingular
  ?accusativePlural

WHERE {
  ?lexeme dct:language wd:Q36510 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularForm .
    ?accusativeSingularForm ontolex:representation ?accusativeSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralForm .
    ?accusativePluralForm ontolex:representation ?accusativePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786 .
  }
}
