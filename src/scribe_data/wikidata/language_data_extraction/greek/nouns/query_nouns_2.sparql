# tool: scribe-data
# All Greek (Q36510) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?singularNominative
  ?singularGenitive
  ?singularAccusative
  ?singularVocative

WHERE {
  ?lexeme dct:language wd:Q36510 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularNominativeForm .
    ?singularNominativeForm ontolex:representation ?singularNominative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q131105 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularGenitiveForm .
    ?singularGenitiveForm ontolex:representation ?singularGenitive ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146233 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularAccusativeForm .
    ?singularAccusativeForm ontolex:representation ?singularAccusative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146078 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularVocativeForm .
    ?singularVocativeForm ontolex:representation ?singularVocative ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q185077 .
  }
}
