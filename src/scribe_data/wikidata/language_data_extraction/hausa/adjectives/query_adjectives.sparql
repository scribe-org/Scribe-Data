# tool: scribe-data
# All Hausa (Q56475) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?plural
  ?feminineSingular
  ?masculineSingular

WHERE {
  ?lexeme dct:language wd:Q56475 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .
    FILTER(lang(?adjective) = "ha")

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
      FILTER(lang(?plural) = "ha")
  }

  # MARK: Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
      FILTER(lang(?feminineSingular) = "ha")
  }

  # MARK: Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
      FILTER(lang(?masculineSingular) = "ha")
  }
}
