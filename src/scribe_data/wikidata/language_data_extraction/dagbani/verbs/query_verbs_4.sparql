# tool: scribe-data
# All Dagbani (Q32238) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?singular
  ?firstPersonSingular
  ?firstPersonPlural
  ?singularPhrase
  ?imperativeSecondPersonSingular
  ?imperativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q32238 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingularForm .
    ?firstPersonSingularForm ontolex:representation ?firstPersonSingular ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralForm .
    ?firstPersonPluralForm ontolex:representation ?firstPersonPlural ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPhraseForm .
    ?singularPhraseForm ontolex:representation ?singularPhrase ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q187931 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonSingularForm .
    ?imperativeSecondPersonSingularForm ontolex:representation ?imperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonPluralForm .
    ?imperativeSecondPersonPluralForm ontolex:representation ?imperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q146786 .
  }
}
