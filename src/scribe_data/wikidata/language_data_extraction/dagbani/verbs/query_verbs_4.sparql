# tool: scribe-data
# All Dagbani (Q32238) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?presentContinuous
  ?singular
  ?presentContinuousPlural
  ?firstPersonSingular
  ?firstPersonPlural
  ?singularPhrase

WHERE {
  ?lexeme dct:language wd:Q32238 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousForm .
    ?presentContinuousForm ontolex:representation ?presentContinuous ;
      wikibase:grammaticalFeature wd:Q7240943 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousPluralForm .
    ?presentContinuousPluralForm ontolex:representation ?presentContinuousPlural ;
      wikibase:grammaticalFeature wd:Q7240943, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingularForm .
    ?firstPersonSingularForm ontolex:representation ?firstPersonSingular ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralForm .
    ?firstPersonPluralForm ontolex:representation ?firstPersonPlural ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPhraseForm .
    ?singularPhraseForm ontolex:representation ?singularPhrase ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q187931 .
  }
}
