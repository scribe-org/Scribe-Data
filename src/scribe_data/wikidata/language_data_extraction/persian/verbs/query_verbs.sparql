# tool: scribe-data
# All Persian (Q9168) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?combinedPresentParticiple
  ?secondPersonSingular
  ?secondPersonPlural
  ?thirdPersonSingular
  ?thirdPersonPlural

WHERE {
  ?lexeme dct:language wd:Q9168 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonSingularForm .
    ?secondPersonSingularForm ontolex:representation ?secondPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?secondPersonPluralForm .
    ?secondPersonPluralForm ontolex:representation ?secondPersonPlural ;
      wikibase:grammaticalFeature wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingularForm .
    ?thirdPersonSingularForm ontolex:representation ?thirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonPluralForm .
    ?thirdPersonPluralForm ontolex:representation ?thirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786 .
  }
}
