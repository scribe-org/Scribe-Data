# tool: scribe-data
# All Ukrainian (Q8798) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?nominativePlural
  ?genitivePlural
  ?dativePlural
  ?accusativePluralInanimate
  ?accusativePluralAnimate

WHERE {
  ?lexeme dct:language wd:Q8798 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativePluralForm .
    ?dativePluralForm ontolex:representation ?dativePlural ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralInanimateForm .
    ?accusativePluralInanimateForm ontolex:representation ?accusativePluralInanimate ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786, wd:Q51927539 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralAnimateForm .
    ?accusativePluralAnimateForm ontolex:representation ?accusativePluralAnimate ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786, wd:Q51927507 .
  }
}
